//= Athena Script ==============================================================
// Ragnarok Online EP6.0 MakeHat Script	by Blaze
//==============================================================================

//============================================================
// 羽のベレー
//------------------------------------------------------------
rachel.gat,152,131,3	script	サキュール	928,{
	if(countitem(5172) < 1 || countitem(7063) < 100 || countitem(982) < 1) {
		mes "[サキュール]";
		mes "腰が引けているぞ！";
		mes "しっかり、背筋を伸ばせ！";
		mes "ムダの無い動きをとる事！";
		mes "それが人間たる者の正しい動きだ！";
		next;
		mes "[サキュール]";
		mes "そう、そこのお前！";
		mes "そうは思わないか！";
		next;
		if(select("はい！　そうです！","いいえ")==2) {
			mes "[サキュール]";
			mes "声が小さい！";
			mes "そんな態度で、";
			mes "この世の中を生き抜こうとするのか！";
			mes "甘い！";
			close;
		}
		mes "[サキュール]";
		mes "よし！";
		mes "今時、珍しい真面目な奴だな！";
		mes "感心したぞ！！";
		next;
		mes "[サキュール]";
		mes "お前には、^FF0000羽のベレー^000000を";
		mes "被る資格がある！";
		next;
		mes "[サキュール]";
		mes "お前の志に免じて、";
		mes "材料を持って来たら、";
		mes "この私が、羽のベレーを作ってやろう。";
		mes "どうだ、わかったか！";
		next;
		mes "[サキュール]";
		mes "材料は、^FF0000ベレー^000000が1個、";
		mes "^FF0000柔らかい羽毛^000000を100個、";
		mes "^FF0000白色の染料^000000を1個だ。";
		mes "これらをちゃんと持って来るんだ！";
		mes "わかったか！";
		next;
		mes "[サキュール]";
		mes "お前の志を評価しての特別な提案だ！";
		mes "ありがたく思うがよい！";
		mes "さぁ！　早く材料を揃えに行くのだ！";
		mes "早くするんだ！";
		close;
	}
	mes "[サキュール]";
	mes "よし！";
	mes "ちゃんと^FF0000羽のベレー^000000の";
	mes "材料を揃えたな！";
	mes "その努力に免じ、この私が";
	mes "直接、作ってやろうではないか！";
	next;
	mes "[サキュール]";
	mes "ちゃんと見ているんだぞ！";
	mes "姿勢を崩すな！";
	mes "待機中だからって、";
	mes "緊張を緩めるな！";
	next;
	mes "[サキュール]";
	mes "いいか！";
	mes "この作業は神聖たるもの！";
	mes "だらしない態度だと、";
	mes "^FF0000低確率だが、失敗する事もある！^000000";
	next;
	mes "[サキュール]";
	mes "^FF0000つまり、せっかくの材料が";
	mes "全部ムダになるのだ！^000000";
	next;
	mes "[サキュール]";
	mes "その責任は、この私にはない……";
	mes "お前にあるのだ！";
	mes "わかったか！";
	mes "では、始めるぞ！";
	next;
	mes "[サキュール]";
	mes "どりゃああああああああ!!";
	mes "-サッサッサッ-";
	mes "-カサカサカサ-";
	mes "-サッサッサッ-";
	mes "-モミモミ-";
	next;
	mes "[サキュール]";
	mes "はああああああああああ!!";
	mes "-サッサッサッ-";
	mes "-カサカサカサ-";
	mes "-サッサッサッ-";
	mes "-モミモミ-";
	next;
	delitem 5172,1;
	delitem 7063,100;
	delitem 982,1;
	if(rand(10) == 0) {
		//失敗未調査
		close;
	}
	getitem 5170,1;
	mes "[サキュール]";
	mes "見たか！　成功だ！";
	mes "この、羽とベレーの絶妙なバランス！";
	mes "これが、今を生きる";
	mes "人々が持つべきのものだろう！";
	next;
	mes "[サキュール]";
	mes "これは、お前が揃えた材料で";
	mes "作り上げたもの。";
	mes "よって、お前のものだ。";
	mes "この羽のベレーに相応しい";
	mes "人間になる事を祈る。";
	close;
}

//============================================================
// モザイク
//------------------------------------------------------------
rachel.gat,91,273,8	script	?	930,{
	if(countitem(983) < 1 || countitem(7111) < 100 || countitem(938) < 99 || Zeny < 100000) {
		mes "[?]";
		mes "……ムッ！";
		mes "俺の事を聞くな！";
		mes "俺がここに居る事、";
		mes "いや、俺という存在を";
		mes "全て忘れるんだ！";
		next;
		mes "[?]";
		mes "……なんだ……";
		mes "俺の同類じゃないか。";
		mes "お前、自分の正体を隠したくて";
		mes "ここまで来たんだろう？";
		mes "……クケケケケケ";
		next;
		mes "[?]";
		mes "仲間には……そうだな……";
		mes "まあ、逃走生活のコツを";
		mes "教えてやってもいいぜ。";
		mes "仲間同士で助け合った方が";
		mes "色々と助かるだろう？";
		next;
		mes "[?]";
		mes "^FF0000黒色の染料^000000を1個、";
		mes "^FF0000なめらかな紙^000000を100個、";
		mes "^FF0000べとべとする液体^000000を99個、";
		mes "そして、^FF000010万Zeny^000000。";
		next;
		mes "[?]";
		mes "こいつらで……クケケケケケ……";
		mes "正体を隠せる良いものを作ってやる。";
		next;
		mes "[?]";
		mes "何してるんだ？";
		mes "早く行け！";
		mes "つるんでいると、目立つだけだ！";
		next;
		mes "[?]";
		mes "隠れる場所は自分で探せ。";
		mes "ここは、俺の場所だ。";
		mes "悪いが譲る気はないぞ！";
		close;
	}
	mes "[?]";
	mes "よう、同類。";
	mes "そうか、俺の言う通り";
	mes "材料を持って来たか！";
	next;
	mes "[?]";
	mes "それなら、正体がバレないアイテムを";
	mes "作ってやろう。";
	next;
	mes "-チョキチョキチョキチョキ-";
	mes "-ぬりぬりぬりぬり-";
	mes "-チョキチョキチョキチョキ-";
	mes "-ぺたぺたぺたぺた-";
	next;
	mes "[?]";
	mes "……見るんじゃない！";
	mes "コツでも奪う気か？";
	mes "俺は、これで食っていかなきゃ";
	mes "いけないんだぞ！";
	next;
	mes "-チョキチョキチョキチョキ-";
	mes "-ぬりぬりぬりぬり-";
	mes "-チョキチョキチョキチョキ-";
	mes "-ぺたぺたぺたぺた-";
	next;
	mes "[?]";
	mes "ふぅ。";
	mes "これで、お前の正体はバレないはずだ！";
	mes "さあ、受け取りな。";
	delitem 983,1;
	delitem 7111,100;
	delitem 938,99;
	set Zeny,Zeny-100000;
	getitem 5175,1;
	next;
	mes "[?]";
	mes "今後も、お前の逃走生活が";
	mes "うまくいくよう祈ってるぜ！";
	mes "クケケケケケ";
	close;
}

//============================================================
// アヤム・使道の帽子
//- Registry -------------------------------------------------
// TKHAT_QUE -> 0〜2
//------------------------------------------------------------
payon_in03.gat,139,124,3	script	ハン・ガラム	88,{
	switch(TKHAT_QUE) {
	case 0:
		if(countitem(7445) < 7 || countitem(7446) < 7 || countitem(7447) < 7 || countitem(7448) < 7) {
			mes "[ハン・ガラム]";
			mes "俺は、ハン・ガラム。";
			mes "貴族の血を受け継いだ男だ……";
			next;
			mes "[ハン・ガラム]";
			mes "貴族の血と言っても、";
			mes "今は何の力も無いが……";
			mes "まあ、昔はそうだったって事だ！";
			next;
			mes "[ハン・ガラム]";
			mes "今や宝石に興味はない。";
			mes "ただ、伝統ある帽子を作る事が";
			mes "俺の唯一の取り得かな。";
			next;
			mes "[ハン・ガラム]";
			mes "まあ、これはこの世で俺にしか";
			mes "出来ない事だから、すごいと言えば";
			mes "すごいかも知れないけどな。";
			next;
			mes "[ハン・ガラム]";
			mes "だが、その伝統ある帽子作りも……";
			mes "最近は、神龍様が現れないから……";
			mes "全然、出来ない状況なんだ。";
			next;
			mes "[ハン・ガラム]";
			mes "神龍様が何かって？";
			mes "あー……";
			mes "それは……";
			mes "うーん……";
			next;
			mes "[ハン・ガラム]";
			mes "神龍様は、俺の元に降りてくる";
			mes "神様なんだ。";
			mes "神様が、降りてくるのさ。";
			next;
			mes "[ハン・ガラム]";
			mes "いきなり空から";
			mes "神龍様が降りて来るのではなく、";
			mes "神龍様を呼び出せるある物を使って";
			mes "ここにお招きするのさ。";
			mes "昔は、たくさん持っていたけど……";
			next;
			mes "[ハン・ガラム]";
			mes "金持ちだった頃は、";
			mes "本当にたくさんあったんだけど……";
			mes "今は、一つも持っていないんだ。";
			next;
			mes "[ハン・ガラム]";
			mes "それで、この唯一の技術も";
			mes "使えなくなったってわけ。";
			mes "なさけないだろう？";
			next;
			mes "[ハン・ガラム]";
			mes "ああ……";
			mes "^FF00004種類の宝玉が、7個ずつあれば^000000";
			mes "神龍様をお招きできるのに……";
			next;
			mes "[ハン・ガラム]";
			mes "^FF0000緑の宝玉、青の宝玉^000000、";
			mes "^FF0000赤の宝玉、黄の宝玉^000000、";
			mes "これらを7個ずつ……";
			mes "全部で、28個の宝玉が必要なんだ……";
			next;
			mes "[ハン・ガラム]";
			mes "すぐ手に入るものでもないし……";
			mes "困ったものだ……";
			close;
		}
		mes "[ハン・ガラム]";
		mes "俺は、ハン・ガラム。";
		mes "貴族の血を受け継いだ……って、あれ？";
		next;
		mes "[ハン・ガラム]";
		mes "この気配は……、まさか宝玉を？";
		mes "4つ全部、しかも7個ずつ!?";
		mes "間違いない！";
		mes "これは、宝玉の気配だ……！";
		next;
		mes "[ハン・ガラム]";
		mes "ほら、ちょっと見せてくれ。";
		mes "な？";
		mes "ちょっとだけ、な？";
		next;
		mes "[ハン・ガラム]";
		mes "…………";
		mes "あぁ、やっぱり持っていたな！";
		mes "珍しいものなのに、";
		mes "よくこんなに持っているな！";
		next;
		mes "[ハン・ガラム]";
		mes "…………ふむ";
		next;
		mes "[ハン・ガラム]";
		mes "な？";
		mes "もし、もし要らないならさ。";
		mes "その宝玉を俺にくれないか？";
		next;
		mes "[ハン・ガラム]";
		mes "俺は、帽子職人だけど……";
		mes "少し事情があって、帽子が作れなく";
		mes "なったんだ……。";
		next;
		mes "[ハン・ガラム]";
		mes "その宝玉を全部貰えれば……";
		mes "そう、お前が持っている";
		mes "その丸い玉だよ。";
		mes "それさえあれば、作れるんだ……";
		next;
		mes "[ハン・ガラム]";
		mes "だから、宝玉を俺にくれないか？";
		mes "その代わり、俺が伝統ある帽子を";
		mes "何か作ってあげるよ！";
		mes "とっても珍しい帽子だよ！";
		mes "どうだ？　欲しくないか？";
		next;
		mes "[ハン・ガラム]";
		mes "その宝玉をくれるなら、";
		mes "帽子を作ってあげるよ！";
		mes "な？　な？";
		next;
		if(select("宝玉をあげる","あげない")==2) {
			mes "[ハン・ガラム]";
			mes "チ……";
			mes "あのさ、物ってのはな？";
			mes "必要とする所で使わないと";
			mes "価値が薄れるんだよ。";
			mes "ったく、わかってないなー。";
			close;
		}
		mes "[ハン・ガラム]";
		mes "おお！　ありがたくもらうよ！";
		mes "約束だから帽子もちゃんと";
		mes "作ってあげよう！";
		next;
		mes "[ハン・ガラム]";
		mes "少し待っててくれ。";
		mes "宝玉から神龍様を";
		mes "呼び出さないと！";
		next;
		mes "[ハン・ガラム]";
		mes "宝玉の神龍様ー！";
		mes "ここにおいでくださいませ！";
		delitem 7445,7;
		delitem 7446,7;
		delitem 7447,7;
		delitem 7448,7;
		set TKHAT_QUE,1;
		close;
	case 1:
		mes "[ハン・ガラム]";
		mes "……";
		next;
		mes "[ハン・ガラム]";
		mes "…………";
		next;
		mes "[ハン・ガラム]";
		mes "………………";
		next;
		mes "[ハン・ガラム]";
		mes "……ハッ、き、来たー！";
		mes "きた、キタ、来たぞー！";
		mes "神龍様が、この俺に降りて下さる！";
		mes "ああー、私にお言葉を下さる！";
		mes "未来を見せてくださる！";
		next;
		misceffect 90;
		mes "[ハン・ガラム]";
		mes "クッ!!!!!!!!!!!!";
		next;
		mes "[ハン・ガラム]";
		mes "……";
		next;
		mes "[ハン・ガラム]";
		mes "…………";
		next;
		mes "[ハン・ガラム]";
		mes "そう……そうなのですね！";
		mes "^FF0000アヤム^000000、使道の帽子^000000は、";
		mes "そう作るのですね！";
		mes "今、その作り方が目に見える！";
		mes "ああ、すぐにでもこの手で作れそうだ！";
		next;
		mes "[ハン・ガラム]";
		mes "アヤムを作る為の材料は、";
		mes "^FF0000輝く鱗^000000が300個！";
		mes "^FF0000損傷したダイヤモンド^000000が5個！";
		mes "^FF0000赤色の染料^000000が1個！";
		mes "^FF0000柔らかい布^000000が50個！";
		next;
		mes "[ハン・ガラム]";
		mes "使道の帽子を作る為の材料は、";
		mes "^FF0000輝く鱗^000000が300個！";
		mes "^FF0000損傷したダイヤモンド^000000が5個！";
		mes "^FF0000黒色の染料^000000が1個！";
		mes "^FF0000ハット[1]^000000が1個！";
		next;
		mes "[ハン・ガラム]";
		mes "それらさえあれば、今すぐ！";
		mes "すぐにでも作れそうだ！";
		next;
		misceffect 7;
		mes "[ハン・ガラム]";
		mes "……";
		next;
		mes "[ハン・ガラム]";
		mes "…………";
		next;
		mes "[ハン・ガラム]";
		mes "……ふぅ……";
		mes "……終わってしまった。";
		next;
		mes "[ハン・ガラム]";
		mes "だが、神龍様が";
		mes "製法を見せてくださったお陰で、";
		mes "作り方は、全部覚えている！";
		mes "うん！　アヤムと使道の帽子だったな？";
		next;
		mes "[ハン・ガラム]";
		mes "材料は、聞いただろう？";
		mes "作りたいものの材料を揃えて来てくれ。";
		next;
		mes "[ハン・ガラム]";
		mes "あぁ、1ついっておくが";
		mes "作りたい方だけの材料を";
		mes "持ってきてくれよ。";
		mes "じゃないと俺の好みと言う事で";
		mes "アヤムを優先して作っちゃうからな。";
		next;
		mes "[ハン・ガラム]";
		mes "材料を持ってくるまで待ってる。";
		mes "じゃ、頑張って！";
		set TKHAT_QUE,2;
		close;
	case 2:
		if(countitem(954) >= 300 && countitem(733) >= 5 && countitem(975) >= 1 && countitem(7166) >= 50) {
			set '@itemid,5174;
			setarray '@need,954,733,975,7166;
			setarray '@amount,300,5,1,50;
			break;
		}
		if(countitem(954) >= 300 && countitem(733) >= 5 && countitem(983) >= 1 && countitem(2221) >= 1) {
			set '@itemid,5173;
			setarray '@need,954,733,983,2221;
			setarray '@amount,300,5,1,1;
			break;
		}
		mes "[ハン・ガラム]";
		mes "アヤムを作る為の材料は、";
		mes "^FF0000輝く鱗^000000が300個！";
		mes "^FF0000損傷したダイヤモンド^000000が5個！";
		mes "^FF0000赤色の染料^000000が1個！";
		mes "^FF0000柔らかい布^000000が50個！";
		mes "以上だ。";
		next;
		mes "[ハン・ガラム]";
		mes "使道の帽子を作る為の材料は、";
		mes "^FF0000輝く鱗^000000が300個！";
		mes "^FF0000損傷したダイヤモンド^000000が5個！";
		mes "^FF0000黒色の染料^000000が1個！";
		mes "^FF0000ハット[1]^000000が1個！";
		mes "以上だ。";
		next;
		mes "[ハン・ガラム]";
		mes "よく覚えておいてくれ。";
		mes "作りたい帽子の材料を";
		mes "集めて来たら、作ってあげるよ。";
		next;
		mes "[ハン・ガラム]";
		mes "あぁ、1ついっておくが";
		mes "作りたい方だけの材料を";
		mes "持ってきてくれよ。";
		mes "じゃないと俺の好みと言う事で";
		mes "アヤムを優先して作っちゃうからな。";
		next;
		mes "[ハン・ガラム]";
		mes "ああ〜、帽子を作りたいなぁ。";
		mes "出来上がったその瞬間の充実感！";
		mes "もう一度味わってみたいなぁ。";
		close;
	}
	mes "[ハン・ガラム]";
	mes "必要な材料を全部揃えたな。";
	mes "思ったより早いじゃないか。";
	next;
	mes "[ハン・ガラム]";
	mes "やはり、器の大きい人は";
	mes "何をしても違うんだな！　ハハハ";
	mes "では、君に" +getitemname('@itemid)+ "を";
	mes "作ってあげよう！";
	next;
	mes "[ハン・ガラム]";
	mes "よし！";
	mes "まずは材料を渡してくれ！";
	next;
	if(select(getitemname('@itemid)+ "の材料を渡す","何も渡さない")==2) {
		mes "[ハン・ガラム]";
		mes "……？";
		mes "何だ？";
		mes "何か理由でもあるのか？";
		close;
	}
	mes "[ハン・ガラム]";
	mes "それじゃあ、あっという間に";
	mes "作ってやるからな！";
	mes "ほんの少し、待っててくれ！";
	next;
	mes "[ハン・ガラム]";
	mes "そうそう……そうだ……";
	mes "ふむ……";
	next;
	mes "[ハン・ガラム]";
	mes "…………";
	next;
	mes "-ハン・ガラムはモクモクと";
	mes "　材料を組み合わせるだけで、";
	mes "　一言もしゃべろうとしない-";
	mes "-ひたすら、彼は帽子を作っている-";
	next;
	mes "[ハン・ガラム]";
	mes "…………";
	next;
	mes "[ハン・ガラム]";
	mes "……ふぅ。";
	mes "久しぶりにやると、流石に疲れるなぁ。";
	next;
	mes "[ハン・ガラム]";
	mes "君のお陰で、";
	mes "久々に腕を震わせてもらったよ。";
	mes "これが、" +getitemname('@itemid)+ "だ。";
	mes "約束通り、これをあげよう。";
	next;
	for(set '@i,0; '@i < 4; set '@i,'@i+1)
		delitem '@need['@i],'@amount['@i];
	set TKHAT_QUE,0;
	getitem '@itemid,1;
	mes "[ハン・ガラム]";
	mes "また、何かを作る為には";
	mes "材料を集めないといけないが……";
	next;
	mes "[ハン・ガラム]";
	mes "まぁ、その時はその時だ。";
	mes "誰かが集めてくれるだろう！";
	mes "ハハハ";
	next;
	mes "[ハン・ガラム]";
	mes "とにかく、ありがとう。";
	mes getitemname('@itemid)+ "を大事に扱ってくれよ。";
	close;
}

//============================================================
// 老人の仮面・花嫁の仮面・獅子の仮面
//- Registry -------------------------------------------------
// TKMASK_QUE -> 0〜2
//------------------------------------------------------------
payon.gat,135,320,3	script	ウォルマン	97,{
	switch(TKMASK_QUE) {
	case 0:
		if(countitem(706) < 1) {
			mes "[ウォルマン]";
			mes "お……";
			mes "お…………";
			mes "俺は……";
			next;
			mes "[ウォルマン]";
			mes "俺には、幸運が必要だ！";
			mes "どうか、この俺に幸運をわけてくれー！";
			mes "幸運ー！";
			mes "幸運と言えば、あれだー！";
			next;
			mes "[ウォルマン]";
			mes "^FF0000四葉のクローバー^000000をくれー！";
			mes "誰か！";
			mes "四葉のクローバーを俺にくれぇー！";
			close;
		}
		mes "[ウォルマン]";
		mes "ちょ、ちょっと待って！";
		mes "そこのあんた！";
		next;
		mes "[ウォルマン]";
		mes "むむむむむ……";
		mes "やっぱり……";
		mes "気配が普通じゃない……";
		next;
		mes "[ウォルマン]";
		mes "もしかして、四葉のクローバーを";
		mes "持っているんじゃないか？";
		mes "もしそうなら";
		mes "俺に一つ、わけてくれないか？";
		next;
		mes "[ウォルマン]";
		mes "ただでくれとは言わない。";
		mes "今すぐは無理だけど、お礼はする！";
		mes "ね？　ね？　いいでしょ？";
		mes "いや、損な話じゃないって！";
		next;
		if(select("四葉のクローバーをあげる","何もあげない")==2) {
			mes "[ウォルマン]";
			mes "そうか……";
			mes "そうだよね……";
			mes "幸運のシンボルを見も知らぬ人に";
			mes "渡すわけがないか……";
			next;
			mes "[ウォルマン]";
			mes "……ハァ……";
			mes "……まったく……";
			mes "世の中って、ホント世知辛いな！";
			close;
		}
		mes "[ウォルマン]";
		mes "ほんと！　本当!?";
		mes "やった！　ありがとう！";
		mes "これで、俺の不運も終わりだ！";
		mes "フハハハハ！";
		next;
		mes "[ウォルマン]";
		mes "あ、そうだ。";
		mes "お礼をするって言ったよね。";
		next;
		mes "[ウォルマン]";
		mes "悪いけど、俺の頼みをもう一つだけ";
		mes "聞いてくれないか？";
		mes "なに、四葉のクローバーを持っていた";
		mes "あんたならすぐに出来るさ！";
		next;
		mes "[ウォルマン]";
		mes "えっと……";
		mes "^FF0000丸太^000000を500個、";
		mes "^FF0000弾力がある縄^000000を10個、";
		mes "集めて来てくれないか？";
		next;
		mes "[ウォルマン]";
		mes "あんたにお礼をするには、";
		mes "これらが必要なんだ。";
		next;
		mes "[ウォルマン]";
		mes "いや、ホント損はしないって！";
		mes "ね？　ね？";
		mes "集めて来てくれよ！";
		mes "頼むからさ！";
		delitem 706,1;
		set TKMASK_QUE,1;
		close;
	case 1:
		if(countitem(7201) < 500 || countitem(7200) < 10) {
			mes "[ウォルマン]";
			mes "だから、集めて欲しいのは";
			mes "^FF0000丸太^000000が500個と";
			mes "^FF0000弾力がある縄^000000が10個だって！";
			mes "今度は、ちゃんと覚えてよ！";
			close;
		}
		mes "[ウォルマン]";
		mes "へぇ、早かったね！";
		mes "こんなに早く集まるなんて、";
		mes "四葉のクローバーのお陰かな？";
		next;
		mes "[ウォルマン]";
		mes "よし！";
		mes "それじゃ約束通り、お礼をするよ！";
		next;
		mes "[ウォルマン]";
		mes "何を隠そう、実は俺、";
		mes "超有名な仮面職人なのだ！";
		next;
		mes "[ウォルマン]";
		mes "全ての仮面、異国の仮面だって";
		mes "ぜーんぶ作れちゃうのさ！";
		mes "すごいだろう？";
		next;
		mes "[ウォルマン]";
		mes "でも最近、何をしても";
		mes "失敗してばっかりだったんだ……";
		next;
		mes "[ウォルマン]";
		mes "しかーし！　それももう終わりだ！";
		mes "この四葉のクローバーで！";
		next;
		mes "[ウォルマン]";
		mes "今の俺は、そう……";
		mes "まさしくスーパーウォルマン！";
		next;
		mes "[ウォルマン]";
		mes "あとは、作って欲しい仮面の材料を";
		mes "持ってきてくれれば、";
		mes "老人の仮面と花嫁の仮面、";
		mes "そして、獅子の仮面。";
		mes "これらの仮面が作れるよ。";
		mes "さぁ、どれが欲しい？";
		next;
		switch(select("老人の仮面","花嫁の仮面","獅子の仮面")) {
		case 1:
			set '@itemid,5176;
			setarray '@need,7015,952,1028;
			setarray '@amount,20,100,100;
			break;
		case 2:
			set '@itemid,5169;
			setarray '@need,1049,1059,1054,1024;
			setarray '@amount,20,500,2,100;
			break;
		case 3:
			set '@itemid,5177;
			setarray '@need,1048,1053,980;
			setarray '@amount,500,2,1;
			break;
		}
		delitem 7201,500;
		delitem 7200,10;
		set TKMASK_QUE,2;
		mes "[ウォルマン]";
		mes "よし！　" +getitemname('@itemid)+ "だな。";
		mes "えっと……";
		next;
		mes "[ウォルマン]";
		set '@max,getarraysize('@need);
		for(set '@i,0; '@i < '@max; set '@i,'@i+1)
			mes "^FF0000" +getitemname('@need['@i])+ "^000000が" +'@amount['@i]+ "個" +('@max == '@i+1? "。": "、");
		mes "材料さえ揃えば、すぐ作ってあげるよ。";
		next;
		mes "[ウォルマン]";
		mes "何、そのがっかりした顔は？";
		mes "まさか、ただで作ってくれるとでも";
		mes "思ったの？";
		mes "勘違いしないでよ。";
		next;
		mes "[ウォルマン]";
		mes "お礼ってのは、";
		mes "この俺が作った仮面を手にする資格が";
		mes "得られるって事だよ。";
		mes "それって、すごい名誉な事なんだぜ！";
		next;
		mes "[ウォルマン]";
		mes "まぁ、お代はもらわないよ。";
		mes "俺だって男だ！";
		mes "材料さえ持って来れば、";
		mes "その場ですぐ作ってあげるよ。";
		next;
		mes "[ウォルマン]";
		mes "じゃ、あんたが持ってきた";
		mes "丸太と弾力のある縄で";
		mes "準備をしておくから、";
		mes "材料を集めてきて。";
		mes "ほら、早く行って。";
		close;
	case 2:
		if(countitem(7015) >= 20 && countitem(952) >= 100 && countitem(1028) >= 100) {
			set '@itemid,5176;
			setarray '@need,7015,952,1028;
			setarray '@amount,20,100,100;
			break;
		}
		if(countitem(1048) >= 500 && countitem(1053) >= 1 && countitem(980) >= 1) {
			set '@itemid,5177;
			setarray '@need,1048,1053,980;
			setarray '@amount,500,2,1;
			break;
		}
		if(countitem(1049) >= 20 && countitem(1059) >= 500 && countitem(1054) >= 2 && countitem(1024) >= 100) {
			set '@itemid,5169;
			setarray '@need,1049,1059,1054,1024;
			setarray '@amount,20,500,2,100;
			break;
		}
		mes "[ウォルマン]";
		mes "まだ材料が揃ってないの？";
		mes "早く揃えて来てくれよ。";
		mes "俺は今、仮面を作りたくて";
		mes "ウズウズしてるんだ。";
		next;
		mes "[ウォルマン]";
		mes "…………あー、";
		mes "どの仮面を作るつもりだったっけ？";
		next;
		switch(select("老人の仮面","花嫁の仮面","獅子の仮面")) {
		case 1:
			set '@itemid,5176;
			setarray '@need,7015,952,1028;
			setarray '@amount,20,100,100;
			break;
		case 2:
			set '@itemid,5169;
			setarray '@need,1049,1059,1054,1024;
			setarray '@amount,20,500,2,100;
			break;
		case 3:
			set '@itemid,5177;
			setarray '@need,1048,1053,980;
			setarray '@amount,500,2,1;
			break;
		}
		mes "[ウォルマン]";
		mes "そうそう、" +getitemname('@itemid)+ "。";
		mes "えっと……";
		next;
		mes "[ウォルマン]";
		set '@max,getarraysize('@need);
		for(set '@i,0; '@i < '@max; set '@i,'@i+1)
			mes "^FF0000" +getitemname('@need['@i])+ "^000000が" +'@amount['@i]+ "個" +('@max == '@i+1? "。": "、");
		mes "材料さえ揃えば、すぐ作ってあげるよ。";
		close;
	}
	mes "[ウォルマン]";
	mes "お！";
	mes getitemname('@itemid)+ "を作る為の材料が";
	mes "全部揃ったんだね。";
	next;
	mes "[ウォルマン]";
	mes "よし、材料を渡してくれ。";
	mes "ちょうど準備も出来てるから";
	mes "すぐに作ってあげるよ。";
	next;
	if(select("材料を渡す","渡さない")==2) {
		mes "[ウォルマン]";
		mes "……何だよ！";
		mes "ここまで来てやる気が無いの？";
		mes "面白くないやつだな！";
		close;
	}
	mes "[ウォルマン]";
	mes "よし！";
	mes "それじゃ、パパっと作ってあげるよ。";
	next;
	mes "[ウォルマン]";
	mes "あ！　そーれ！";
	mes "-ヨイッショーヨイヨイ-";
	mes "-ヨイッショーヨイヨイ-";
	mes "-ヨイッショーヨイヨイ-";
	mes "-モミモミモミ-";
	next;
	mes "[ウォルマン]";
	mes "もいっちょー！";
	mes "-ヨイッショーヨイヨイ-";
	mes "-ヨイッショーヨイヨイ-";
	mes "-ヨイッショーヨイヨイ-";
	mes "-モミモミモミ-";
	next;
	mes "[ウォルマン]";
	mes "出来た！";
	mes "ま、これぐらい仮面職人としては";
	mes "簡単すぎる事だけどさ。";
	mes "ほら、あんたが欲しがってた";
	mes getitemname('@itemid)+ "だよ。";
	set '@max,getarraysize('@need);
	for(set '@i,0; '@i < '@max; set '@i,'@i+1)
		delitem '@need['@i],'@amount['@i];
	set TKMASK_QUE,0;
	getitem '@itemid,1;
	next;
	mes "[ウォルマン]";
	mes "じゃ、次の作業の準備があるから。";
	mes "またねー";
	close;
}

//============================================================
// ヴァルキリーヘルム
//------------------------------------------------------------
hugel.gat,147,130,3	script	ゲニルヘイマン	897,{
	mes "[ゲニルヘイマン]";
	mes "生きる事に置いて、";
	mes "意味の無い行動は存在しない。";
	mes "君とワシが出会った事も、";
	mes "そういう運命だからだ。";
	next;
	mes "[ゲニルヘイマン]";
	mes "意味があるとは言え、";
	mes "今の君には、興味が沸かない。";
	mes "失礼だとは思うが、";
	mes "ワシは、金持ちが好きでな……";
	next;
	mes "[ゲニルヘイマン]";
	mes "君の所持金が増えれば、";
	mes "ワシの興味も沸いてくるかも知れない。";
	mes "まあ、その時はその時だな。";
	mes "ハハハハハ";
	close;
}
