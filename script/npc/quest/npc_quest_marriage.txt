//====================================================================
//Ragnarok Online EP4.0(J) Marriage System      V1.0
//
//  ■ @event_marry -> 受付フラグ 0or1
//  ■ @defend -> タイムアップフラグ、嫁にのみ仕込む
//
//	未調査箇所があまりにも多すぎるので適当に補完してます by Rayce
//====================================================================
prt_church.gat,94,100,4	script	結婚コンパニオン	71,{
	cutin "wedding_marry01",2;
	mes "[ハッピーマリー]";
	mes "結婚とは、幸せを夢見る";
	mes "全ての人々の希望です。";
	mes "おやおや、もしかして……";
	mes "あなた、結婚したい方が";
	mes "いらっしゃるのですか？";
	next;
	switch (select("結婚式について聞きたい","結婚式ってどうやるんですか","結婚式を申し込みたい","私は無敵のソロ軍団！")) {
	case 1:
		mes "[ハッピーマリー]";
		mes "結婚はルーンミッドガッツ王国の";
		mes "国王であるトリスタン3世陛下が";
		mes "すべての新郎新婦に祝福を与えたいと";
		mes "式を全て執り行っております。";
		next;
		mes "[ハッピーマリー]";
		mes "国政にお忙しい方ですが、新しく門出を";
		mes "迎える方々のため、このプロンテラ";
		mes "大聖堂に、わざわざ御出でくださって";
		mes "いるのです。";
		next;
		mes "[ハッピーマリー]";
		mes "まず、結婚についてですが……慎重に";
		mes "行ってください。";
		mes "一度結婚すると、二度と別れることが";
		mes "出来ないからです。結婚した二人は";
		mes "例え一人がこの世を去ったとしても";
		mes "永遠に一緒なのです。";
		next;
		mes "[ハッピーマリー]";
		mes "男性は女性と、女性は男性と結婚する";
		mes "ことができます。同性やモンスターと";
		mes "結婚したいと主張される方がいるかも";
		mes "しれませんが……陛下がお認めに";
		mes "ならないので、ご了承くださいませ。";
		next;
		mes "[ハッピーマリー]";
		mes "未来を約束した相手がいらっしゃい";
		mes "ましたら、プロポーズしてみたら";
		mes "いかがですか？";
		mes "幸せな幸せな未来を夢見る";
		mes "すべての恋人に祝福あれ!!";
		close2;
		cutin "wedding_marry01",255;
		end;
	case 2:
		mes "[ハッピーマリー]";
		mes "まず、新郎新婦は私に結婚の申し込みを";
		mes "してください。申し込まれましたら";
		mes "二人だけのパーティーを組み、国王";
		mes "陛下に会いに行ってください。";
		next;
		mes "[ハッピーマリー]";
		mes "国王陛下へは、最初に^0000FF男性^000000が話しかけて";
		mes "ください。そして結婚したい女性の名前";
		mes "を正確にお伝えください。";
		next;
		mes "[ハッピーマリー]";
		mes "新郎が報告を終えましたら、次は新婦の";
		mes "番です。国王陛下に新郎の名前を正確に";
		mes "お伝えください。";
		next;
		mes "[ハッピーマリー]";
		mes "お互い結婚相手の名前を報告し終えると";
		mes "指輪交換が行われ、その瞬間、二人は";
		mes "永遠の愛の証を得たこととなります。";
		next;
		mes "[ハッピーマリー]";
		mes "結婚を申し込む方がたくさんいる場合、";
		mes "順番をお待ちいただくことになります。";
		mes "陛下のお体は1つですので、同時に";
		mes "複数の式を行うことは出来ないのです。";
		next;
		mes "[ハッピーマリー]";
		mes "そして、陛下へご報告を行う際は、";
		mes "^ff0000最初に新郎が声を掛けてから3分以内に";
		mes "すべての過程を終わらせてください。^000000";
		mes "もし3分以内に式が終わらなかった場合";
		mes "二度と結婚することは出来ません。";
		next;
		mes "[ハッピーマリー]";
		mes "また、陛下に結婚相手の名前を報告";
		mes "する時は、必ず正確にお願いします。";
		mes "少しでも報告間違いがありますと";
		mes "やはり式に支障をきたしてしまいます。";
		next;
		mes "[ハッピーマリー]";
		mes "式の事前に、必ず結婚相手の名前の";
		mes "正確な綴りをご確認ください。";
		next;
		mes "[ハッピーマリー]";
		mes "方法としましては、";
		mes "相手の方に耳打ちを行ってみると";
		mes "よろしいと思います。相手に通じれば";
		mes "その名前が正しい綴りですし、もし通じ";
		mes "なければ、名前が間違っている可能性が";
		mes "あります。";
		next;
		mes "[ハッピーマリー]";
		mes "まず、相手に一度耳打ちをして、";
		mes "チャットウィンドウ左部のボタンを";
		mes "押します。そうすると、";
		mes "耳打ちリストに相手の名前が";
		mes "表示されますので選択しましょう。";
		next;
		mes "[ハッピーマリー]";
		mes "相手の名前がボタンの左に";
		mes "表示されますので、その名前を";
		mes "Ctrl+Cでコピーしておきます。";
		mes "そうすれば、報告の際に";
		mes "Shift＋Insertで相手の名前を";
		mes "呼び出すことができます。";
		next;
		mes "[ハッピーマリー]";
		mes "1)相手と耳打ち会話をする";
		mes "2)耳打ちリストに表示された";
		mes "相手の名前を選択する";
		mes "3)チャットウィンドウの左下に";
		mes "表示された相手の名前を";
		mes "Ctrl+Cでコピーする。";
		next;
		mes "[ハッピーマリー]";
		mes "この方法で、相手の名前を";
		mes "確実に覚えることができますし、";
		mes "陛下に報告する時にも";
		mes "使っていただけると思います。";
		next;
		mes "[ハッピーマリー]";
		mes "説明だけですとなんですので、1回";
		mes "試してみましょうか。";
		next;
		mes "[ハッピーマリー]";
		mes "Ctrl＋Cでコピーしたものを";
		mes "Shift＋Insertキーで呼び出してみて";
		mes "ください。よろしいですか？";
		next;
		input '@test$;
		mes "[ハッピーマリー]";
		mes "どうです、出来ましたか？";
		next;
		mes "[ハッピーマリー]";
		mes "では、もし結婚したいと思うお相手が";
		mes "いましたら、申し込みを是非どうぞ。";
		close2;
		cutin "wedding_marry01",255;
		end;
	case 3:
		cutin "wedding_marry02",2;
		mes "[ハッピーマリー]";
		mes "結婚をするためには、いくつか条件が";
		mes "ございます。";
		next;
		mes "[ハッピーマリー]";
		mes "まず、結婚するには^3377FFベースレベルが";
		mes "45以上^000000でなければなりません。";
		mes "そして、新郎新婦二人とも、申し込みを";
		mes "していただく必要がございます。";
		next;
		mes "[ハッピーマリー]";
		mes "申し込みの際、男性は^3377FF1,300,000zeny^000000と";
		mes "^3377FFタキシード1着^000000、女性は^3377FF1,200,000zeny^000000と";
		mes "^3377FFウェディングドレス1着^000000が必要です。";
		next;
		mes "[ハッピーマリー]";
		mes "あと、結婚指輪を準備しなければなりま";
		mes "せんので、新郎新婦は1個ずつ、^3377FFダイヤ";
		mes "の指輪^000000を準備してください。";
		mes "新郎が1つ、新婦が1つですよ。";
		mes "よろしいですね。";
		next;
		mes "[ハッピーマリー]";
		mes "以上の品が全てそろっているのを確認";
		mes "しましたら、申し込み書に御自分の名前";
		mes "を記入していただきます。";
		next;
		mes "[ハッピーマリー]";
		mes "二人とも申し込みが済みましたら";
		mes "結婚式を挙げることができます。";
		mes "申し込みをなさいますか？";
		next;
		if(select("はい","いいえ")==2) {
			mes "[ハッピーマリー]";
			mes "今日も幸せな一日にな〜っれっ！";
			close2;
			cutin "wedding_marry02",255;
			end;
		}
		if(PartnerId) {
			mes "[ハッピーマリー]";
			mes "もう結婚しておられるようですね。";
			mes "重婚は陛下が認めておられません。";
			next;
			mes "[ハッピーマリー]";
			mes "それ以前に、一生を共にするという";
			mes "約束を破るということが、どのような";
			mes "意味であるか……相手のお気持ちを";
			mes "考えたことがありまして？";
			next;
			mes "[ハッピーマリー]";
			mes "結婚は一度きりです。例え相手が";
			mes "死んだとしても、誓いは永遠なのです。";
			close2;
			cutin "wedding_marry02",255;
			end;
		}
		if(Upper == UPPER_BABY) {
			//養子のときは？未調査
			close2;
			cutin "wedding_marry02",255;
			end;
		}
		if(@event_marry) {
			mes "[ハッピーマリー]";
			mes "おや、すでに申し込みをされているよう";
			mes "ですね。この奥に陛下がいらっしゃい";
			mes "ますので、陛下へと赴き、式の順番を";
			mes "お待ちください。";
			close2;
			cutin "wedding_marry02",255;
			end;
		}
		if(BaseLevel<45) {
			mes "[ハッピーマリー]";
			mes "プロポーズなさるより先に、ご自分の";
			mes "鍛練が必要なようです。結婚には義務や";
			mes "責任が招じます。相手の方を守れるくら";
			mes "い強くなってから、いらして下さい。";
			close2;
			cutin "wedding_marry02",255;
			end;
		}
		if(countitem(2613)<1) {
			mes "[ハッピーマリー]";
			mes "結婚指輪に使うダイヤの指輪を";
			mes "忘れていらっしゃったようですね。";
			mes "ダイヤの指輪をお持ちになってから";
			mes "またお申し込みください。";
			close2;
			cutin "wedding_marry02",255;
			end;
		}
		if((Sex==0 && countitem(2338)<1) || (Sex==1 && countitem(7170)<1)) {
			mes "[ハッピーマリー]";
			if(Sex==0) {
				mes "おや、ウェディングドレスはどうなさい";
				mes "ましたか？式を挙げるのに、花嫁がドレ";
				mes "ス無しでは……一生に一度の事ですし、";
			}
			else {
				mes "おや、タキシードはどうなさいました？";
				mes "式を挙げるのに、花婿がそのような出で";
				mes "立ちでは……一生に一度の事ですし、";
			}
			mes "忘れずに準備してきてくださいませ。";
			close2;
			cutin "wedding_marry02",255;
			end;
		}
		if((Sex==0 && Zeny<1200000) || (Sex==1 && Zeny<1300000)) {
			mes "[ハッピーマリー]";
			mes "申し訳ございませんが、結婚式の費用と";
			if(Sex==0)
				mes "して1,200,000zeny必要です。";
			else
				mes "して1,300,000zeny必要です。";
			close;
		}
		mes "[ハッピーマリー]";
		mes "御結婚お祝い申し上げます！";
		next;
		mes "[ハッピーマリー]";
		mes "それでは、これが申し込み書です。";
		mes "ここに自分の名前を記入してください。";
		while(1) {
			next;
			input '@name$;
			if('@name$ != strcharinfo(0)) {
				mes "[ハッピーマリー]";
				mes "まぁまぁ！";
				mes "自分の名前を正しく書くことが出来ない";
				mes "のですか。結婚するつもりなら、自分の";
				mes "名前くらい正しく名乗れるようになって";
				mes "ください……もう一度、ここに自分の名";
				mes "前をちゃんと正確に記入してください。";
				continue;
			}
			break;
		}
		delitem 2613,1;
		if(Sex==0) {
			delitem 2338,1;
			set Zeny,Zeny-1200000;
		}
		else {
			delitem 7170,1;
			set Zeny,Zeny-1300000;
		}
		set @event_marry,1;
		mes "[ハッピーマリー]";
		mes "はい、申し込み書を承りました。";
		mes "後程、陛下には結婚相手の名前も報告";
		mes "することになります。式の前に相手の";
		mes "名前を正確に言えるよう、準備して";
		mes "おくことをお勧めいたします。";
		next;
		mes "[ハッピーマリー]";
		mes "では、結婚相手の方も申し込みを済ませ";
		mes "ましたら、二人だけでパーティーを組み";
		mes "この奥にいます国王陛下の元へどうぞ。";
		next;
		mes "[ハッピーマリー]";
		mes "そして……お幸せに〜";
		emotion 3;
		close2;
		cutin "wedding_marry02",255;
		end;
	case 4:
		cutin "wedding_marry02",2;
		emotion 23;
		hideoffnpc "ソロ軍団#プロンテラ";
		hideoffnpc "ソロ軍団#ゲフェン";
		hideoffnpc "ソロ軍団#モロク";
		hideoffnpc "ソロ軍団#フェイヨン";
		hideoffnpc "ソロ軍団#アマツ";
		hideoffnpc "ソロ軍団#コンロン";
		emotion 27,"ソロ軍団#プロンテラ";
		emotion 27,"ソロ軍団#ゲフェン";
		emotion 27,"ソロ軍団#モロク";
		emotion 27,"ソロ軍団#フェイヨン";
		emotion 27,"ソロ軍団#アマツ";
		emotion 27,"ソロ軍団#コンロン";
		mes "[ソロ軍団]";
		mes "精錬は1人で出来ちゃうぞ！";
		mes "ダンジョンでパーティーは贅沢だ！";
		mes "私には心に決めたNPCがいる！";
		mes "生まれて転職まで独りでした！";
		mes "クリスマスにはシングルベ〜ル！";
		mes "私たちは無敵のソロ軍団！";
		next;
		hideonnpc "ソロ軍団#プロンテラ";
		hideonnpc "ソロ軍団#ゲフェン";
		hideonnpc "ソロ軍団#モロク";
		hideonnpc "ソロ軍団#フェイヨン";
		hideonnpc "ソロ軍団#アマツ";
		hideonnpc "ソロ軍団#コンロン";
		emotion 4;
		close2;
		cutin "wedding_marry02",255;
		end;
	}
OnInit:
	hideonnpc "ソロ軍団#プロンテラ";
	hideonnpc "ソロ軍団#ゲフェン";
	hideonnpc "ソロ軍団#モロク";
	hideonnpc "ソロ軍団#フェイヨン";
	hideonnpc "ソロ軍団#アマツ";
	hideonnpc "ソロ軍団#コンロン";
	end;
}

//========================================================================================
prt_church.gat,99,125,4	script	国王陛下	108,{
	//1なら問答無用で弾く、2で且つ嫁以外なら弾く
	if('flag==1 || ('flag==2 && strcharinfo(0) != 'bride$)) {
		mes "[トリスタン3世]";
		mes "他の式が進行中だ。";
		mes "順番をお待ちなさい。";
		close;
	}
	if(PartnerId) {
		mes "[トリスタン3世]";
		mes "いつまでも、お幸せに。";
		close;
	}
	if(@event_marry==0) {
		mes "[トリスタン3世]";
		mes "結婚を望むなら、まずは申し込みから";
		mes "済ませてきなさい。";
		close;
	}
	if(Sex==1) {	//申し込みは新郎から
		getpartymember getcharid(1);
		for(set '@i,0; '@i<2; set '@i,'@i+1) {
			if($@partymembername$['@i] != strcharinfo(0))
				set '@another$,$@partymembername$['@i];		//PT内に居る相手の名前を一時保存
		}
		if($@partymembercount != 2 || readparam(Sex,'@another$) == 1) {	//2人PTじゃない、もしくは相手が男かオフライン
			mes "[トリスタン3世]";
			mes "結婚を望むなら、式を挙げる二人で";
			mes "パーティーを結成しなさい。";
			close;
		}
		if(readparam(Upper,'@another$) == UPPER_BABY) {
			//相手が養子なら？未調査
			close;
		}
		set 'flag,1;
		initnpctimer;	//手続きは3分以内
		set 'groom$,strcharinfo(0);
		mes "[トリスタン3世]";
		mes "幸せな未来を夢見る若者よ。";
		mes "これから二人で歩む人生が幸せである";
		mes "よう、共に過ごす時が至宝となるよう";
		mes "二人の時が永遠となるよう……";
		next;
		announce "国王陛下: 新郎" +'groom$+ "よ、汝に問う……",9;
		mes "[トリスタン3世]";
		mes 'groom$+ "よ。";
		mes "汝の将来を明るく照らし、生涯を";
		mes "共に歩まんとする女性の名前を";
		mes "ここに告げよ。";
		next;
		input 'bride$;
		if('bride$ != '@another$) {	//メッセージこれでいいの？
			announce "国王陛下: 準備不足のようだ……次の式を行うとしよう。",9;
			stopnpctimer;
			set 'flag,0;
			close;
		}
		//もし嫁がログインしてるなら仕込まれたタイムアップフラグを堕ろす
		if(attachrid(getcharid(3,'bride$))) {
			set @defend,0;
			detachrid;
		}
		if(attachrid(getcharid(3,'groom$))) {	//再度アタッチ
			//PTが解体されていようが嫁がすでにログアウト状態であろうが関係なし！性転換は考慮しない
			announce "国王陛下: 汝、その富める時も、貧しき時も、病めるときも、健やかなる時も",9;
			mes "[トリスタン3世]";
			mes "新郎" +'groom$+ "よ。";
			mes "汝、その富める時も、貧しき時も、";
			mes "病めるときも、健やかなる時も、";
			mes "" +'bride$+ "を愛し";
			mes "敬い、慰め、助け、その命の限り";
			mes "堅く節操を守ることを誓うか。";
			next;
			announce "国王陛下: " +'bride$+ "を愛し、敬い、慰め、助け、その命の限り堅く節操を守ることを誓うか",9;
			menu "誓います",-;
			announce "国王陛下: 新郎" +'groom$+ "が新婦" +'bride$+ "に愛を誓った……",9;
			mes "[トリスタン3世]";
			mes "うむ。では、新婦に問うてみよう。";
			set 'flag,2;
			set @event_marry,0;	//フラグ解放
		}
		close;
	}
	//新婦側の処理
	if('flag==0) {
		mes "[トリスタン3世]";
		mes "先ず、新郎から私に報告なさい。";
		close;
	}
	mes "[トリスタン3世]";
	mes "幸せな未来を夢見る若者よ。";
	mes "これから二人で歩む人生が幸せである";
	mes "よう、共に過ごす時が至宝となるよう";
	mes "二人の時が永遠となるよう……";
	next;
	announce "国王陛下: 新婦" +'bride$+ "よ、汝に問う……",9;
	mes "[トリスタン3世]";
	mes "" + 'bride$+ "よ。";
	mes "汝を常に守り、生涯を共に";
	mes "歩まんとする男性の名前は";
	mes "" +'groom$+ "か。";
	next;
	if(select("はい","いいえ")==2) {
		//メッセージ未調査？
		mes "[トリスタン3世]";
		mes "そうか";
		announce "国王陛下: 新婦が嫌みたいだ……次の式を行うとしよう。",9;
		stopnpctimer;
		set 'flag,0;
		close;
	}
	announce "国王陛下: 汝、その富める時も、貧しき時も、病めるときも、健やかなる時も",9;
	mes "[トリスタン3世]";
	mes "新婦" +'bride$+ "よ。";
	mes "汝、その富める時も、貧しき時も、";
	mes "病めるときも、健やかなる時も、";
	mes 'groom$+ "を愛し";
	mes "敬い、慰め、助け、その命の限り";
	mes "堅く節操を守ることを誓うか。";
	next;
	announce "国王陛下: " +'groom$+ "を愛し、敬い、慰め、助け、その命の限り堅く節操を守ることを誓うか",9;
	if(select("誓います","……やっぱりやめます")==2) {
		//メッセージ未調査？
		mes "[トリスタン3世]";
		mes "そうか";
		announce "国王陛下: 新婦が嫌みたいだ……次の式を行うとしよう。",9;
		stopnpctimer;
		set 'flag,0;
		close;
	}
	if(@defend) {	//タイムアップしてるかどうか最終チェック
		set @event_marry,0;
		mes "Time up...";
		close;
	}
	//結婚処理
	if(getcharid(3,'groom$)<=0) {	//新郎がマリッジブルーでログアウトしてたら指輪渡せない！
		stopnpctimer;
		set 'flag,0;
		set @event_marry,0;
		mes "Groom not exist";
		close;
	}
	set '@dummy,marriage('groom$);
	getitem 2635,1,0;
	getitem 2634,1,0,getcharid(3,'groom$);
	set 'flag,0;
	set @event_marry,0;
	sc_start SC_WEDDING,3600000,0;
	sc_start SC_WEDDING,3600000,0,getcharid(3,'groom$);
	wedding;
	stopnpctimer;
	announce "国王陛下: 新郎" +'groom$+ "と新婦" +'bride$+ "、新たなる夫婦の誕生をここに祝福しよう！",9;
	mes "[トリスタン三世]";
	mes "ルーン=ミッドガッツ王国が国王";
	mes "トリスタン三世の名において、汝ら";
	mes "二人の未来を祝福しよう。";
	next;
	mes "[トリスタン三世]";
	mes "二人とも……幸せにな……";
	close;
//新郎が話して3分以内に結婚成立しないとき。(この状態になってもまた話せば結婚可能？)
OnTimer180000:
	announce "国王陛下: 返事が遅い……次の式を行うとしよう。",9;
	set 'flag,0;
	//タイムアップの場合は嫁にフラグを仕込む
	//結婚処理直前にチェックすることで、結婚処理が重複するのを防止！
	if(attachrid(getcharid(3,'bride$)))
		set @defend,1;
	end;

/* 以下のメッセージは出ないはずですが…？
   嫁が居ないとか性別判定とかはすでにやってる。 by Rayce
LError2:
	mes "[トリスタン3世]";
	mes "Error!";
	mes "嫁が存在しません";
	mapannounce "prt_church.gat","国王陛下: 準備不足のようだ……次の式を行うとしよう。",8;
	stopnpctimer;
	set $@marry,0;
	close;
LError3:
	mes "[トリスタン3世]";
	mes "Error!";
	mes "性別判定で不具合が出ました";
	mes "本人Readparam:"+readparam(21,strcharinfo(0));
	mes "相手Readparam:"+readparam(21,$@NameWoman$);
	mapannounce "prt_church.gat","国王陛下: 準備不足のようだ……次の式を行うとしよう。",8;
	stopnpctimer;
	set $@marry,0;
	close;
*/
}

//=======================Solo軍団==============================
prt_church.gat,97,102,0	script	ソロ軍団#プロンテラ	105,{
	mes "[ソロ軍団]";
	mes "精錬は1人で出来ちゃうぞ！";
	close;
}

prt_church.gat,98,102,0	script	ソロ軍団#ゲフェン	705,{
	mes "[ソロ軍団]";
	mes "ダンジョンでパーティーは贅沢だ！";
	close;
}

prt_church.gat,99,102,0	script	ソロ軍団#モロク		707,{
	mes "[ソロ軍団]";
	mes "私には心に決めたNPCがいる！";
	close;
}

prt_church.gat,100,102,0	script	ソロ軍団#フェイヨン	708,{
	mes "[ソロ軍団]";
	mes "生まれて転職まで独りでした！";
	close;
}

prt_church.gat,101,102,0	script	ソロ軍団#アマツ	767,{
	mes "[ソロ軍団]";
	mes "クリスマスにはシングルベ〜ル！";
	close;
}

prt_church.gat,102,102,0	script	ソロ軍団#コンロン	780,{
	mes "[ソロ軍団]";
	mes "私たちは無敵のソロ軍団！";
	close;
}
