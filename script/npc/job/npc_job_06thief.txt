//====================================================================
//Ragnarok Online Thief jobchange script
//
//　■ CHANGE_TF  -> 0〜3
//     OLD_CLASS  -> 転生前の職業
//     @doubt_npc -> 0〜4
//====================================================================

//==========================================
// 試験申請および転職
//------------------------------------------

moc_prydb1.gat,39,129,2	script	ギルド関係者	69,{
	if(Job == Job_Thief) {
		mes "[シーフギルド関係者]";
		mes "よー。";
		mes "悩みがあったらいつでも";
		mes "このおねーさんに相談してよ！";
		mes "うん？";
		close;
	}
	if(Job != Job_Novice) {
		mes "[シーフギルド関係者]";
		mes "お前はなんだ……？";
		next;
		mes "[シーフギルド関係者]";
		switch(Job) {
			case Job_Swordman: mes "馬鹿な剣士か！"; 		break;
			case Job_Magician: mes "サーカス団の魔術師か！"; 	break;
			case Job_Archer:   mes "アホウのアーチャーだな！"; 	break;
			case Job_Acolyte:  mes "世界最強芸能人崇拝者アコか！"; 	break;
			case Job_Merchant: mes "悪質な商人か！"; 		break;
		}
		if(Job < Job_Thief) {
			next;
			mes "[シーフギルド関係者]";
		}
		mes (Sex)? "おい、兄ちゃん。": "ほら、姉ちゃん。";
	   	mes "何しに来た？帰れ、帰れ！";
		close;
	}
	if(Upper == UPPER_HIGH && OLD_CLASS != Job_Assassin && OLD_CLASS != Job_Rogue) {
		//転生前が盗賊系でないとき、未調査？
		close;
	}
	switch(CHANGE_TF) {
	case 0:
		mes "[シーフギルド関係者]";
		if(Sex == 0) {
			mes "へえ…お嬢様が何の用で";
			mes "こんな汚い所まで";
			mes "いらっしゃたかな？";
		}
		else {
			mes "へぇ…坊ちゃまが何の用で";
			mes "こんな汚い所までわざわざ";
			mes "来たのかな？";
		}
		break;
	case 1:
		mes "[シーフギルド関係者]";
		mes "おーまた来たか。";
		mes "今度は間違いないだろう？";
		break;
	case 2:
		mes "[シーフギルド関係者]";
		mes "ここで何してるんだ？";
		mes "キノコ盗りに行かないのか？";
		mes "もしかして、転職条件を忘れた？";
		mes "もう一度教えようか？";
		next;
		if(select("うん。","ううん。")==2) {
			mes "[シーフギルド関係者]";
			mes "…なんだよ？";
			mes "特に用もないのに話し掛けて";
			mes "…喧嘩売ってるのかよ？うん？";
			close;
		}
		mes "[シーフギルド関係者]";
		mes "まったく、しっかり覚えとけよ。";
		mes "いつもお前みたいな奴が1人は";
		mes "出るからな。";
		next;
		mes "[シーフギルド関係者]";
		mes "モロクで、一番悪質で有名な商人";
		mes "<シブ>が営んでいるキノコ農場に";
		mes "行って、そこでキノコを盗んで来るのが";
		mes "転職試験だ！盗るキノコは^0000ff紅の毛網キノ";
		mes "コ^000000と";
		mes " ^0000ff紅の毛ベトベトキノコ^000000 の２種類";
		next;
		mes "[シーフギルド関係者]";
		mes "キノコ農場にはキノコだけでなく、";
		mes "モンスターもいるから";
		mes "気をつけることだ。";
		next;
		mes "[シーフギルド関係者]";
		mes "キノコを盗ってまたここに戻ったら、";
		mes "採点をする。";
		mes "<紅の毛網キノコ>は１つで３点、";
		mes "<紅の毛ベトベトキノコ>は１つで";
		mes "１点。全部あわせて２５点以上";
		mes "だったら合格だ。";
		next;
		mes "[シーフギルド関係者]";
		mes "行き方は…ピラミッドを出てから";
		mes "町の方に行った先の、２つの";
		mes "柱の間に隠されているうちの仲間に";
		mes "頼んだらいい。奴に話し掛けたら、";
		mes "キノコの裏道に送ってくれるから。";
		next;
		mes "[シーフギルド関係者]";
		mes "正確な位置は多分…^ff0000141. 125^000000'";
		mes "だったかな？";
		mes "お前の現在位置が知りたいなら、";
		mes "</where>というコマンドを";
		mes "書くと、位置が表示されるから、";
		mes "参考にしなよ。";
		close;
	case 3:
		mes "[シーフギルド関係者]";
		mes "お？言ってみただけなのに、";
		mes "本当にキノコを取ってきた？";
		mes "お前、アホウだろう？";
		mes "騙されたんだよ。わかる？";
		next;
		mes "[シーフギルド関係者]";
		mes "なーんちゃって！ウソだよー。";
		mes "ここはシーフになろうとする";
		mes "バカモノ達がくる所だ。";
		mes "転職したいなら、隣の奴に聞いてくれ。";
		close;
	}
	//case0,1の続き
	next;
	if(select("シーフになりたい。","なんでもない。")==2)
		close;
	if(CHANGE_TF == 0) {
		mes "[シーフギルド関係者]";
		mes "…自慢する事はないだろう？";
		mes "シーフになりたいって、そんな";
		mes "恥ずかしい事がよく言えるな。";
		mes "馬鹿か？道歩いてる人に私は";
		mes "シーフになりますって、広告でもする";
		mes "気か？ちょっと、頭に何がある？";
		next;
		mes "[シーフギルド関係者]";
		mes "本当にシーフになりたいのか？";
		next;
		switch (select("うん。","ううん。","そんなこと言ってる君は？")) {
		case 1:
			mes "[シーフギルド関係者]";
			mes "ふん…";
			mes "まあ、そーだったら仕方ないか。";
			break;
		case 2:
			mes "[シーフギルド関係者]";
			mes "だったら、なんだよ？ここに";
			mes "恋人でもいるのか？じゃなかったら";
			mes "シーフギルドまで何しに来た？";
			break;
		case 3:
			mes "[シーフギルド関係者]";
			mes "え…？あたし…？";
			mes "まあ、あたしは…ただ…うう、";
			mes "細かい事は聞くな！";
			break;
		}
		mes "とりあえず、現実世界でシーフに";
		mes "なりたいとか言ったら駄目だよ！";
		next;
		mes "[シーフギルド関係者]";
		mes "本当に、";
		mes "本当にシーフになりたいのか？";
		next;
		if(select("うん。","やっぱりやめる。")==2) {
			mes "[シーフギルド関係者]";
			mes "なんだよ。転職もしないのに…";
			mes "本当に何しに来た？";
			mes "何を考えているんだよ…？";
			close;
		}
		mes "[シーフギルド関係者]";
		mes "だったら君の名前を言って。";
		mes "うん？なに？";
		mes strcharinfo(0)+ "." +strcharinfo(0)+ " って…？";
		next;
		if(Upper == UPPER_HIGH) {
			if(getskilllv(1) < 9 || SkillPoint) {
				mes "[シーフギルド関係者]";
				mes "シーフになりたいという";
				mes "君の意志はわかるが…";
				mes "意志だけで世の中生きて";
				mes "行けないんだよ！最低でも";
				mes "初心者スキル９にならないと駄目だ！";
				close;
			}
			mes "[シーフギルド関係者]";
			mes "あなたは、既に転生した身";
			mes "転職試験はいりませんね";
			next;
			unequip;
			jobchange Job_Thief,UPPER_HIGH;
			skill 149,1,0;
			skill 150,1,0;
			skill 151,1,0;
			skill 152,1,0;
			mes "[シーフギルド関係者]";
			mes "転職おめでとうございます！";
			mes "これからもギルドの";
			mes "復興のため、頑張ってください！";
			close;
		}
		set CHANGE_TF,1;
		mes "[シーフギルド関係者]";
		mes "申請書はOK。";
		mes "転職条件をもう知ってるんだったら、";
		mes "このまま面接に入るよ。";
		mes "やってみるか？";
		next;
		if(select("うん、いいよ。","いや、後で。")==2) {
			mes "[シーフギルド関係者]";
			mes "まあ、好きにして。ばいばい〜。";
			close;
		}
	}
	else {
		mes "[シーフギルド関係者]";
		mes "さあ、それでは、確認しようか？";
		next;
	}
	mes "[シーフギルド関係者]";
	mes "名前が… " +strcharinfo(0)+ "。えっと…";
	mes "あ、これだな。姓名 " +strcharinfo(0)+ "。";
	mes "ちょっと〜待って〜。";
	next;
	if(getskilllv(1) < 9 || SkillPoint) {
		mes "[シーフギルド関係者]";
		mes "シーフになりたいという";
		mes "君の意志はわかるが…";
		mes "意志だけで世の中生きて";
		mes "行けないんだよ！最低でも";
		mes "初心者スキル９にならないと駄目だ！";
		close;
	}
	mes "[シーフギルド関係者]";
	mes "うん…資料を見たらある程度興味";
	mes "深い部分もあるし…基本能力も";
	mes "出来てるし…いいよ。";
	mes "姓名 " +strcharinfo(0)+ "。これで、";
	mes "シーフギルドの面接は終わり！";
	next;
	mes "[シーフギルド関係者]";
	mes "さあ、今度はシーフとしての";
	mes "才能があるかどうかだ。";
	mes "試験については知ってるか？";
	next;
	if(select("もちろん知ってる。","そんなこと知ってるわけないだろう。")==1) {
		mes "[シーフギルド関係者]";
		mes "そう？よかったな。";
	}
	else {
		mes "[シーフギルド関係者]";
		mes "知らない？仕方ないな。";
		mes "よく聞けよ。";
		next;
		mes "[シーフギルド関係者]";
		mes "モロクで、一番悪質で有名な商人";
		mes "<シブ>が営んでいるキノコ農場に";
		mes "行って、そこでキノコを盗んで来るのが";
		mes "転職試験だ！盗るキノコは^0000ff紅の毛網キノ";
		mes "コ^000000と";
		mes " ^0000ff紅の毛ベトベトキノコ^000000 の２種類";
		next;
		mes "[シーフギルド関係者]";
		mes "キノコを盗ってまたここに戻ったら、";
		mes "採点をする。";
		mes "<紅の毛網キノコ>は１つで３点、";
		mes "<紅の毛ベトベトキノコ>は１つで";
		mes "１点。全部あわせて２５点以上";
		mes "だったら合格だ。";
		next;
		mes "[シーフギルド関係者]";
		mes "行き方は…ピラミッドを出てから";
		mes "町の方に行った先の、２つの";
		mes "柱の間に隠されているうちの仲間に";
		mes "頼んだらいい。奴に話し掛けたら、";
		mes "キノコの裏道に送ってくれるから。";
		next;
		mes "[シーフギルド関係者]";
		mes "正確な位置は多分…^ff0000141. 125^000000'";
		mes "だったかな？";
		mes "お前の現在位置が知りたいなら、";
		mes "</where>というコマンドを";
		mes "書くと、位置が表示されるから、";
		mes "参考にしなよ。";
	}
	next;
	mes "[シーフギルド関係者]";
	mes "行く前に忠告したいのは、";
	next;
	mes "[シーフギルド関係者]";
	mes "キノコ農場にはモンスターがいる";
	mes "ことだ。…知ってるか？遊び気分で";
	mes "行ったら、痛い目に遭うから、";
	mes "気を引き締めて行った方がいい。";
	next;
	mes "[シーフギルド関係者]";
	mes "君の判断でモンスターを倒したり、";
	mes "逃げたりして、適当にやって行く事だ。";
	next;
	mes "[シーフギルド関係者]";
	mes "次はキノコだけど、転職試験といえ、";
	mes "君がこれからする事は犯罪だから。";
	mes "転職のためには悪になるしか";
	mes "ないって事だ。それが出来なかったら";
	mes "シーフの資格はない。そーだろう？";
	mes "ほら、はっきりケジメをつけろ。";
	next;
	mes "[シーフギルド関係者]";
	mes "この以上あたしが言う事はない。";
	mes "そろそろ出発して！";
	set CHANGE_TF,2;
	close;
}


//==========================================
// キノコ採集
//------------------------------------------

moc_ruins.gat,141,125,3	script	怪しい奴	83,{
	if(Job == Job_Thief) {
		mes "[シーフギルド関係者]";
		mes "へえ、立派なシーフになったな。";
		mes "でも、ここはもう入れないよ。";
		mes "君も知ってると思うけど、ここは";
		mes "転職希望の奴しか入れない";
		mes "試験場だから。もっと面白い所へ、";
		mes "美味しい物を拾いに行ってくれ。";
		close;
	}
	if(Job != Job_Novice) {
		switch(@doubt_npc) {
		case 0:
			mes "[怪しい奴]";
			mes "こんにちはーいい天気ですね！";
			mes "今日みたいに暖かい陽射の気持ちいい午後はピラミッド付近でピクニックするのが一番だよね。";
			break;
		case 1:
			mes "[怪しい奴]";
			mes "'帰る家があるということは幸せの第一歩である。";
			mes "私が一番好きなことわざです。帰る家か…";
			break;
		case 2:
			mes "[怪しい奴]";
			mes "............... ";
			break;
		case 3:
			mes "[怪しい奴]";
			mes "おー私はあ、や、し、い、人ではありません。";
			mes "そんな目で見ないでく、だ、さ、い。";
			break;
		case 4:
			mes "[怪しい奴]";
			mes "…しつこいな。";
			mes "もう話すこともないからあっち行けよ。";
			close;
		}
		set @doubt_npc,@doubt_npc+1;
		close;
	}
	switch(CHANGE_TF) {
	case 0:
	case 1:
		mes "[怪しい奴]";
		mes "そこの初心者！素晴らしい";
		mes "人生を送りたいか？もっと楽な生活を";
		mes "したいか？ならばシーフになれ！";
		mes "そしてシーフギルドに入団するのだ！";
		next;
		mes "[怪しい奴]";
		mes "シーフギルドはいつも君達を";
		mes "歓迎する！今すぐ加入したら";
		mes "ポイントカード付き！(冗談)";
		mes "場所は、ピラミッドダンジョン1階";
		mes "中央の階段を下りて左だ！右かも";
		mes "しれない！まっすぐ行ったら駄目！";
		close;
	case 2:
		set CHANGE_TF,3;
		mes "[シーフギルド関係者]";
		mes "君…妙な匂いがする…";
		mes "転職の試験を受けに来たな。";
		mes "名前は？ " +strcharinfo(0)+ " か…";
		mes "え…うん、名簿にあるな。よし！";
		mes "送るぞ！しかし、どこに落ちるかは";
		mes "知らないよ！イェイー！";
		break;
	case 3:
		mes "[シーフギルド関係者]";
		mes "うはははは！なんだー君、";
		mes "また転職出来なかった？アホウかよ！";
		mes "くはははは！あっ、まあ、シーフは";
		mes "みんな馬鹿しか来ないからな！";
		mes "くはははは！";
		mes "よし！送るぞ！";
		break;
	}
	close2;
	warp "job_thief1.gat",38,50;
	end;
}


//==========================================
// 転職
//------------------------------------------

moc_prydb1.gat,42,133,2	script	ギルド関係者	118,{
	if(Job == Job_Thief) {
		mes "[ブレード]";
		mes "今は特にイベントがないんだ。";
		mes "連絡したら集合することだ。";
		close;
	}
	if(Job != Job_Novice) {
		mes "[シーフギルド関係者]";
		mes "おーい、おーい、そこの奴！";
		mes "ここで何してる？";
		mes "ここはお前の居場所がないから、";
		mes "早く出てもらおう。";
		close;
	}
	switch(CHANGE_TF) {
	case 0:
	case 1:
		mes "[シーフギルド関係者]";
		mes "うん？";
		mes "初心者のくせにここに何の用かな？";
		mes "もし、シーフに転職したいなら、";
		mes "俺の隣の姉ちゃんに聞けよ。";
		close;
	case 2:
		mes "[シーフギルド関係者]";
		mes "へっ、面接は無事に終わったか？";
		mes "次はキノコ盗りか。";
		mes "せいぜい頑張る事だ。";
		close;
	case 3:
		mes "[シーフギルド関係者]";
		mes "うん…？あ、戻ったな。さあ、";
		mes "キノコ見せてよ。点数つけてやるから。";
		next;
		mes "[シーフギルド関係者]";
		setarray '@amount,countitem(1069),countitem(1070);
		if('@amount[0])
			mes "紅の毛網キノコ " +'@amount[0]+ " 個。";
		if('@amount[1])
			mes "紅の毛ベトベトキノコ " +'@amount[1]+ " 個。";
		set '@total,'@amount[0]*3 + '@amount[1];
		mes "評価結果…え… " +'@total+ " 点だ。";
		next;
		if('@total < 25) {
			mes "[シーフギルド関係者]";
			mes "まだまだだっ！もっと盗って来い！";
			close;
		}
		mes "[シーフギルド関係者]";
		mes "よし、合格だ！";
		mes "これからギルドの転職儀式を行う！";
		next;
		delitem 1069,'@amount[0];
		delitem 1070,'@amount[1];
		unequip;
		jobchange Job_Thief;
		set CHANGE_TF,0;
		mes "[シーフギルド総帥 'E']";
		mes "「姓名 " +strcharinfo(0)+ " " +(Sex? "殿": "嬢")+ "。」";
		mes "全ての試験をパスしたので、";
		mes "君をシーフとして認める。同時に";
		mes "シーフギルドへの正式入団を許可する。";
		next;
		mes "[シーフギルド総帥 'E']";
		mes "シーフ転職をお祝いする。";
		mes "以後、シーフギルドの一員として、";
		mes "ギルドの秩序と規範を守れ。万が一、";
		mes "はぐれた行動やルール違反などで、";
		mes "シーフギルドに被害があった場合、";
		next;
		mes "[シーフギルド総帥 'E']";
		mes "君の存在はギルドによって";
		mes "抹消される事をくれぐれも忘れずに。";
		mes "まあ、素晴らしい活動を期待する。";
		mes "以上。";
		next;
		mes "[ブレード]";
		mes "はははは！入団おめでとう！";
		mes "わが同士よ！俺の名は<ブレード>、";
		mes "ここシーフギルドの転職と";
		mes "会員管理担当だ。これから";
		mes "顔合わせる機会が多いと思うから、";
		mes "よろしく。";
		next;
		set Zeny,Zeny+50+'@total*10;
		mes "[ブレード]";
		mes "さあ、これ。";
		mes "新米に支給される補助金と";
		mes "キノコの代金だ。";
		next;
		mes "[ブレード]";
		mes "大体終わったから、帰ってもいい。";
		mes "俺は忙しいんだ。";
		mes "それじゃ、またな。";
		close;
	}
}
