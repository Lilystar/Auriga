//====================================================================
//Ragnarok Online Magician jobchange script
//
//　■ CHANGE_MG -> 0〜4
//     OLD_CLASS -> 転生前の職業
//====================================================================

//==========================================
// 試験申請および転職
//------------------------------------------

geffen_in.gat,164,124,4	script	ギルド関係者	123,{
	mes "[魔術師ギルド関係者]";
	mes "はい、何の用で来ましたの？";
	next;
	switch (select("マジシャンに転職","マジシャンの転職条件を聞く","やめる")) {
	case 1:
		if(Job == Job_Magician) {
			mes "[魔術師ギルド関係者]";
			mes "えー。あなたはもうマジシャンでしょ？";
			mes "自分の事もわからないの？";
			mes "まあ、いつか素手で焚き火を";
			mes "作れる日が来たら。自ら";
			mes "マジシャンの運命というものが、";
			mes "わかってくるんじゃないの？";
			close;
		}
		if(Job != Job_Novice) {
			mes "[魔術師ギルド関係者]";
			mes "えー。あなたは、";
			mes "マジシャンに転職できないよ。";
			mes "既に他の職業に就いているでしょ？";
			close;
		}
		if(Upper == UPPER_HIGH && OLD_CLASS != Job_Wizard && OLD_CLASS != Job_Sage) {
			//転生前が魔術師系でないとき、未調査？
			close;
		}
		break;
	case 2:
		mes "[魔術師ギルド関係者]";
		mes "マジシャンになりたいって？";
		mes (Sex? "兄": "姉")+ "ちゃんみたいな可愛いタイプが、";
		mes "ギルドの一員になって";
		mes "くれるんだったら、";
		mes "当然大歓迎〜。";
		next;
		mes "[魔術師ギルド関係者]";
		mes "まずは、初心者スキルレベル９以上に";
		mes "ならなければならない。";
		mes "その次は、マジシャン転職試験用の";
		mes "公式混合液を作ること。";
		next;
		mes "[魔術師ギルド関係者]";
		mes "どんな混合液を作ればいいのかは、";
		mes "マジシャン転職申請をしたら、";
		mes "教えてあ・げ・る。";
		next;
		mes "[魔術師ギルド関係者]";
		mes "この二つの転職条件を満たしたら、";
		mes "私に話し掛けること。";
		close;
	case 3:
		mes "[魔術師ギルド関係者]";
		mes "うん……？何ー？";
		close;
	}
	//case1の続き
	if(CHANGE_MG == 0) {
		mes "[魔術師ギルド関係者]";
		mes "マジシャンに転職したいと？";
		mes "うん……。";
		next;
		mes "[魔術師ギルド関係者]";
		if(Sex == 0) {
			mes "……よく見ると姉ちゃん、可愛いね。";
			mes "私は姉ちゃんみたいなタイプが大好き！";
		}
		else {
			mes "……よく見たら兄ちゃん、可愛いね。";
			mes "私のタイプじゃないけど……。";
		}
		mes "まあ、それはそれとして、";
		mes "マジシャンに転職したいと言ったよね？";
		mes "だったら、ここのマジシャン";
		mes "転職申請書にサインをお願い。";
		next;
		if(select("マジシャン転職申請をする","やっぱりやめる")==2) {
			mes "[魔術師ギルド関係者]";
			mes "なによ！さっきはマジシャンに";
			mes "なりたいって言ったんじゃないの？";
			mes "ぷんぷん！";
			close;
		}
		if(Upper == UPPER_HIGH) {
			if(getskilllv(1) < 9 || SkillPoint) {
				mes "[魔術師ギルド関係者]";
				mes "……そうなの？準備は全部終わった？";
				mes "……何よ。準備どころか、";
				mes "初心者スキルレベルもまだまだ";
				mes "じゃないの。まず、初心者スキルレベル";
				mes "を上げてから来てよ。";
				close;
			}
			mes "[魔術師ギルド関係者]";
			mes "あなたは、既に転生した身";
			mes "転職試験はいりませんね";
			next;
			unequip;
			jobchange Job_Magician,UPPER_HIGH;
			skill 157,1,0;
			mes "[魔術師ギルド関係者]";
			mes "転職おめでとうございます！";
			mes "これからもギルドの";
			mes "復興のため、頑張ってください！";
			close;
		}
		mes "[魔術師ギルド関係者]";
		mes "うん、そこにサインして……";
		mes "うわー字が綺麗ねー。";
		mes "うん、うん、そこまで。";
		mes "名前は……" +strcharinfo(0)+ "ね。";
		next;
		set CHANGE_MG,rand(1,4);
		mes "[魔術師ギルド関係者]";
		mes "マジシャン転職試験として……";
		mes "マジシャン転職試験用の";
		switch(CHANGE_MG) {
			case 1: mes "公式混合液<１>を作って"; break;
			case 2: mes "公式混合液<２>を作って"; break;
			case 3: mes "公式混合液<３>を作って"; break;
			case 4: mes "公式混合液<４>を作って"; break;
		}
		mes "いただきますー！";
		next;
		getitem 1092,1;
		mes "[魔術師ギルド関係者]";
		mes "混合液を作る方法は、本に書いて";
		mes "あるから、自分で調べて。";
		mes "アイテムを合成するには";
		mes "あの中央の機械を使うんだ。";
		mes "じゃ、頑張ってね〜。";
		close;
	}
	if(getskilllv(1) < 9 || SkillPoint) {
		mes "[魔術師ギルド関係者]";
		mes "……そうなの？準備は全部終わった？";
		mes "……何よ。準備どころか、";
		mes "初心者スキルレベルもまだまだ";
		mes "じゃないの。まず、初心者スキルレベル";
		mes "を上げてから来てよ。";
		close;
	}
	mes "[魔術師ギルド関係者]";
	mes "……そうなの？準備は全部終わった？";
	mes strcharinfo(0)+ "の転職試験は……";
	mes "マジシャン転職試験用の公式";
	switch(CHANGE_MG) {
		case 1:
			set '@num$,"１";
			set '@id,1071;
			break;
		case 2:
			set '@num$,"２";
			set '@id,1085;
			break;
		case 3:
			set '@num$,"３";
			set '@id,1086;
			break;
		case 4:
			set '@num$,"４";
			set '@id,1087;
			break;
	}
	mes "混合液<" +'@num$+ ">を作ることだったよね？";
	mes "じゃ、そうしたら、確認しようか？";
	next;
	if(countitem(1090)) {
		delitem 1090,1;
		delitem '@id,1;
		mes "[魔術師ギルド関係者]";
		mes "……違うの。これを作ってという";
		mes "話ではなくて……あなたの試験は……";
		mes "マジシャン転職用の公式";
		mes "混合液<" +'@num$+ ">を作ることだったの。";
		mes "最初からやり直すしかないわね……。";
		close;
	}
	if(countitem('@id) < 1) {
		mes "[魔術師ギルド関係者]";
		mes "その前に、混合液が入っている";
		mes "試験管はどこにあるの……？";
		mes "持ってこないと評価できないよ。";
		close;
	}
	delitem '@id,1;
	mes "[魔術師ギルド関係者]";
	mes "……ふん、思ってた以上に頑張った";
	mes "みたいね。まあ、完璧ではないけれど、";
	mes "このくらいなら……まあ……いいよ。";
	mes "マジシャン転職試験は、合格ね。";
	mes "あなたが望んだマジシャンになれー！";
	next;
	unequip;
	jobchange Job_Magician;
	set CHANGE_MG,0;
	mes "[魔術師ギルド関係者]";
	mes "魔術師ギルドへようこそ！";
	mes "まあ、改めて挨拶しただけだから、";
	mes "そんなに照れなくてもいいのにー。";
	mes "言ってみただけなのよ、もー。";
	next;
	mes "[魔術師ギルド関係者]";
	mes "とりあえず、私達魔術師ギルドの";
	mes "一員になったね。おめでとうー！";
	mes "これからもよろしくぅー！えへへ〜。";
	close;
}

//==============================================================
geffen_in.gat,164,109,2	script	魔導師	64,{
	mes "[魔導師]";
	mes "魔法の強力な力は因果律によって";
	mes "調節されているのじゃ。";
	next;
	mes "[魔導師]";
	mes "要するに、誰かに害を与えたり、";
	mes "または自然の流れに反する魔法を";
	mes "使った場合、必ずその使用者に";
	mes "反作用が戻ってくるのじゃ。";
	next;
	mes "[魔導師]";
	mes "しかし、その術者の代わりに";
	mes "その反作用を受けてくれる物があれば、";
	mes "安全に魔法が使えるのじゃ。";
	mes "それが｢ジェムストーン｣と呼ばれる";
	mes "神秘の鉱石なのじゃ。";
	close;
}


//==========================================
// アイテム合成
//------------------------------------------

geffen_in.gat,177,112,0	script	本棚	111,{
	mes "[アイテム合成書]";
	mes "この本は、ゲフェン魔法学校の";
	mes "財産です。綺麗に使って下さい。";
	next;
	switch (select("混合液＜１＞","混合液＜２＞","混合液＜３＞","混合液＜４＞","やめる")) {
	case 1:
		mes "[マジシャン転職試験用の";
		mes "公式混合液<１>]";
		mes "* 材料リスト *";
		mes " - ゼロピー 2 個";
		mes " - 綿毛 3 個";
		mes " - ミルク 1 個";
		next;
		mes "[マジシャン転職試験用の";
		mes "公式混合液<１>]";
		mes "* 溶媒 *";
		mes "フェイヨン水溶液";
		mes "発見:場所:";
		mes "フェイヨン弓手村の小さな泉";
		next;
		mes "[マジシャン転職試験用の";
		mes "公式混合液<１>]";
		mes "* 魔法の粉 *";
		mes " - 8472";
		next;
		mes "[マジシャン転職試験用の";
		mes "公式混合液<１>]";
		mes "* 触媒物質 *";
		mes " - イエロージェムストーン";
		mes "(アイテム合成機に内臓されています)";
		close;
	case 2:
		mes "[マジシャン転職試験用の";
		mes "公式混合液<２>]";
		mes "* 材料リスト *";
		mes " - ゼロピー 3 個";
		mes " - 綿毛 1 個";
		mes " - ミルク 1 個";
		next;
		mes "[マジシャン転職試験用の";
		mes "公式混合液<２>]";
		mes "* 溶媒 *";
		mes " - 必要なし";
		next;
		mes "[マジシャン転職試験用の";
		mes "公式混合液<２>]";
		mes "* 魔法の粉 *";
		mes " - 3735";
		next;
		mes "[マジシャン転職試験用の";
		mes "公式混合液<２>]";
		mes "* 触媒物質 *";
		mes " - レッドジェムストーン";
		mes "(アイテム合成機に内臓されています)";
		close;
	case 3:
		mes "[マジシャン転職試験用の";
		mes "公式混合液<３>]";
		mes "* 材料リスト *";
		mes " - ゼロピー 6 個";
		mes " - 綿毛 1 個";
		next;
		mes "[マジシャン転職試験用の";
		mes "公式混合液<３>]";
		mes "* 溶媒 *";
		mes "フェイヨン水溶液";
		mes "発見:場所:";
		mes "フェイヨン弓手村の小さな泉";
		next;
		mes "[マジシャン転職試験用の";
		mes "公式混合液<３>]";
		mes "* 魔法の粉 *";
		mes " - 2750";
		next;
		mes "[マジシャン転職試験用の";
		mes "公式混合液<３>]";
		mes "* 触媒物質 *";
		mes " - ブルージェムストーン";
		mes "(アイテム合成機に内臓されています)";
		close;
	case 4:
		mes "[マジシャン転職試験用の";
		mes "公式混合液<４>]";
		mes "* 材料リスト *";
		mes " - ゼロピー 2 個";
		mes " - 綿毛 3 個";
		next;
		mes "[マジシャン転職試験用の";
		mes "公式混合液<４>]";
		mes "* 溶媒 *";
		mes "モロク水溶液";
		mes "発見:場所:";
		mes "モロクピラミッド入り口辺りの";
		mes "小さなオアシス";
		next;
		mes "[マジシャン転職試験用の";
		mes "公式混合液<４>]";
		mes "* 魔法の粉 *";
		mes " - 5429";
		next;
		mes "[マジシャン転職試験用の";
		mes "公式混合液<４>]";
		mes "* 触媒物質 *";
		mes " - 透明な宝石";
		mes "(アイテム合成機に内臓されています)";
		close;
	case 5:
		close;
	}
}

//==============================================================
geffen_in.gat,164,112,0	script	アイテム合成機	111,{
	mes "[アイテム合成機]";
	mes "マジシャン転職試験用アイテム合成機。";
	mes "この機器は、ゲフェン魔法学校の";
	mes "財産です。綺麗に使って下さい。";
	next;
	if(select("合成機を使う","やめる")==2)
		close;
	mes "[アイテム合成機]";
	mes "合成する水溶液を";
	mes "選んでください。";
	next;
	switch (select("フェイヨン水溶液","モロク水溶液","スキップ")) {
		case 1:	set '@water,1089; 	break;
		case 2: set '@water,1088; 	break;
		case 3: set '@water,0;		break;	//ダミー
	}
	if('@water && countitem('@water) < 1) {
		mes "[アイテム合成機]";
		mes "アイテムが見つかりません。試験管が";
		mes "正常にセッティングされたか確認して";
		mes "下さい。合成を終了します。";
		mes "ご利用ありがとうございます。";
		close;
	}
	while(1) {
		mes "[アイテム合成機]";
		mes "合成するアイテムを";
		mes "選択してください";
		next;	//本鯖はnextが抜けてます。
		while(1) {
			set '@i,select("ゼロピー","綿毛","ミルク","設定したアイテムリストを見る")-1;
			if('@i==3)
				break;
			while(1) {
				input '@num['@i];
				if('@num['@i] < 0 || '@num['@i] > 10000) {
					mes "[アイテム合成機]";
					mes "アイテムを入れる限度を超えました。";
					mes "10000個以下に設定して下さい。";
					next;
					continue;
				}
				break;
			}
		}
		next;
		mes "[アイテム合成機]";
		if('@num[0] <= countitem(909))
			mes "ゼロピー： " +'@num[0]+ " 個";
		if('@num[1] <= countitem(914))
			mes "綿毛： " +'@num[1]+ " 個";
		if('@num[2] <= countitem(519))
			mes "ミルク： " +'@num[2]+ " 個";
		switch('@water) {
			case 1089:
				mes "溶媒液 : フェイヨン水溶液";
				break;
			case 1088:
				mes "溶媒液 : モロク水溶液";
				break;
			case 0:
				//アイテムを何も入れなかった場合はこの台詞1行のみが表示されます。
				if(getarraysize('@num) == 0) {
					mes "何も入っていません";
					next;
					continue;
				}
				mes "溶媒液 : なし";
				break;
		}
		next;
		mes "[アイテム合成機]";
		mes "アイテムを合成しますか？";
		mes "または、アイテム設定を続けますか？";
		next;
		switch (select("アイテムを合成する","アイテム設定を続ける","初期化する")) {
			case 1:
				mes "[アイテム合成機]";
				mes "合成のため、設定したアイテムを";
				mes "合成箱に入れて下さい。";
				mes "1回入れたアイテムは、二度と数などを";
				mes "調節したり、取り戻す事が";
				mes "出来ませんので、ご注意下さい。";
				next;
				mes "[アイテム合成機]";
				mes "もう一度確認した上、確実だと";
				mes "判断したなら<合成開始>ボタンを、";
				mes "やめるなら<終了>ボタンを";
				mes "押して下さい。";
				next;
				if(select("<合成開始>","<終了>")==1)
					break;
				//終了ならfall through
			case 2:
				continue;
			case 3:
				cleararray '@num,0,3;
				mes "[アイテム合成機]";
				mes "アイテム設定が初期化されました。";
				mes "合成機を引き続き、";
				mes "お使いになりますか？";
				next;
				if(select("はい","いいえ")==2) {
					mes "[アイテム合成機]";
					mes "合成を終了します。";
					mes "ご利用ありがとうございます。";
					close;
				}
				continue;
		}
		break;	//case1のみwhile文を抜ける
	}
	mes "[アイテム合成機]";
	mes "設定されたアイテムを";
	mes "合成機に入れています。";
	mes "しばらくお待ち下さい。";
	next;
	if('@num[0] <= countitem(909))
		set '@itemid,909;
	else if('@num[1] <= countitem(914))
		set '@itemid,914;
	else if('@num[2] <= countitem(519))
		set '@itemid,519;
	else
		set '@itemid,0;
	delitem '@water,1;
	delitem 909,'@num[0];
	delitem 914,'@num[1];
	delitem 519,'@num[2];
	mes "[アイテム合成機]";
	mes "設定通りのアイテムを合成箱に";
	mes "入れました。合成箱を閉めます。";
	next;
	mes "[アイテム合成機]";
	mes "合成する魔法の粉の一連番号を";
	mes "入力して下さい。";
	next;
	while(1) {
		input '@magic;
		if(('@magic != 0 && '@magic <= 1000) || '@magic >= 10000) {
			mes "[アイテム合成機]";
			mes "一連番号の入力範囲を超えました。";
			mes "もう一度入力してください。";
			next;
			continue;
		}
		if('@magic == 0) {
			mes "[アイテム合成機]";
			mes "魔法の粉を入れずにスキップしますか？";
		}
		else {
			mes "[アイテム合成機]";
			mes "一連番号 #" +'@magic+ " 魔法の粉。";
			mes "よろしいですか？";
		}
		next;
		if(select("はい","いいえ")==1)
			break;
	}
	mes "[アイテム合成機]";
	mes "最後に、触媒石を選択してください。";
	next;
	set '@stone,select("レッドジェムストーン","イエロージェムストーン","ブルージェムストーン","透明な宝石","スキップ");
	mes "[アイテム合成機]";
	mes "全ての準備が終わったので、";
	mes "これから合成を開始します。";
	mes "しばらくお待ちください。";
	next;
	mes "[アイテム合成機]";
	mes "- 今日のことわざ -";
	switch(rand(3)) {
	case 0:
		mes "｢横槍を入れる｣";
		mes "”人の話や仕事に、第三者が横から口を";
		mes "出して妨げる”という意味です。";
		break;
	case 1:
		mes "｢ただより高い物はない｣";
		mes "”ただで物をもらうと、その代わりに";
		mes "何かを頼まれたり、お礼にお金が";
		mes "かかったりして、かえって高くつく”";
		mes "という意味です。";
		break;
	case 2:
		mes "｢一寸先は闇｣";
		mes "”未来のことは、全く予測することが";
		mes "出来ない”という意味です。";
		break;
	}
	next;
	if('@water == 1089 && '@num[0] == 2 && '@num[1] == 3 && '@num[2] == 1 && '@magic == 8472 && '@stone == 2) {
		getitem 1071,1;
		mes "マジシャン転職試験用の";
		mes "公式混合液 <１>";
	}
	else if('@water == 0 && '@num[0] == 3 && '@num[1] == 1 && '@num[2] == 1 && '@magic == 3735 && '@stone == 1) {
		getitem 1085,1;
		mes "マジシャン転職試験用の";
		mes "公式混合液 <２>";
	}
	else if('@water == 1089 && '@num[0] == 6 && '@num[1] == 1 && '@num[2] == 0 && '@magic == 2750 && '@stone == 3) {
		getitem 1086,1;
		mes "マジシャン転職試験用の";
		mes "公式混合液 <３>";
	}
	else if('@water == 1088 && '@num[0] == 2 && '@num[1] == 3 && '@num[2] == 0 && '@magic == 5429 && '@stone == 4) {
		getitem 1087,1;
		mes "マジシャン転職試験用の";
		mes "公式混合液 <４>";
	}
	else {
		getitem 1090,1;
		mes "マジシャン転職試験用の公式";
		mes "混合液に何かの問題が発生しました。";
	}
	next;
	mes "[アイテム合成機]";
	mes "合成が完了しました。";
	mes "合成を終了します。";
	mes "ご利用ありがとうございます。";
	close;
}


//==========================================
// 水溶液アルバイト
//------------------------------------------

-	script	水溶液アルバイト	88,{
	mes "[魔術師ギルド関係者]";
	mes "水溶液を買いますか？";
	mes " 50 Zenyと空の試験管1つ以上が";
	mes "必要です。";
	next;
	if(select("水溶液を買う","やめる")==2)
		close;
	if(countitem(1092) < 1) {
		mes "[魔術師ギルド関係者]";
		mes "水溶液をどうやって";
		mes "持っていきますか？";
		mes "空の試験管を持ってきて下さい。";
		close;
	}
	if(Zeny < 50) {
		mes "[魔術師ギルド関係者]";
		mes "お金が足りませんよ。";
		close;
	}
	set Zeny,Zeny-50;
	delitem 1092,1;
	if(strnpcinfo(2) == "Pay")
		getitem 1089,1;
	if(strnpcinfo(2) == "Moc")
		getitem 1088,1;
	mes "[魔術師ギルド関係者]";
	mes "ありがとうございました。";
	close;
}

pay_arche.gat,120,100,0	duplicate(水溶液アルバイト)	水溶液アルバイト#Pay	88
moc_ruins.gat,91,150,0	duplicate(水溶液アルバイト)	水溶液アルバイト#Moc	93
