//====================================================================
//Ragnarok Online Merchant jobchange script
//
//　■ CHANGE_MC
//	1	プロンテラA	（2485741）
//	2	プロンテラB	（2328137）
//	3	ゲフェンA	（2989396）
//	4	ゲフェンB	（2191737）
//	5	モロクA		（3012685）
//	6	モロクB		（3487372）
//	7	バイラン島A	（3318702）
//	8	バイラン島B	（3543625）
//	9	プロンテラA	荷物受け取り
//	10	プロンテラB	荷物受け取り
//	11	ゲフェンA	荷物受け取り
//	12	ゲフェンB	荷物受け取り
//	13	モロクA		荷物受け取り
//	14	モロクB		荷物受け取り
//	15	バイラン島A	荷物受け取り
//	16	バイラン島B	荷物受け取り
//	17	プロンテラA	配達完了
//	18	プロンテラB	配達完了
//	19	ゲフェンA	配達完了
//	20	ゲフェンB	配達完了
//	21	モロクA		配達完了
//	22	モロクB		配達完了
//	23	バイラン島A	配達完了
//	24	バイラン島B	配達完了
//	25	試験合格
//	26	試験失敗
//
//　■ TEMPVAL_MC
//	+1	加入料分割支払い
//	+2	マンスの手紙を受け取り
//	+4	マンスの手紙を配達完了
//
//　■ OLD_CLASS -> 転生前の職業
//		領収証のIDは1073〜1080（1073はBS転職と重複）
//====================================================================

//==========================================
// 試験申請および転職
//------------------------------------------

alberta_in.gat,53,43,6	script	商人組合員	86,{
	switch(CHANGE_MC) {
	case 0:
		mes "[組合員マンス]";
		mes "俺に何か用でも？";
		next;
		switch (select("商人になりたいんです","商人って何？","商人転職条件は？","何でもない")) {
			case 1:
				if(Class == 5) {
		   			mes "[組合員マンス]";
		   			mes "…？うん？なにを言ってる？";
					mes "君はもう商人じゃない…まったく。";
		   			mes "ここって笑う所だったの？";
					close;
				}
				break;
			case 2:
				mes "[組合員マンス]";
				mes "商人？簡単に言うと、物を売って";
				mes "お金を稼ぐ。それが商人だ。";
				mes "戦闘が得意なわけでないし、";
				mes "大した攻撃・回復スキルもない。";
				mes "でも、安い値段でアイテムが手に入る。";
				next;
				mes "[組合員マンス]";
				mes "「メマーナイト」という究極の技が";
				mes "あるけれど…まあ、それはちょっと";
				mes "別として…弓、杖、両手剣以外";
				mes "大体の武器も使えるし…";
				mes "と、とにかく、商人はお金稼ぎの";
				mes "ための職業だ。それで充分だろう？";
				close;
			case 3:
				mes "[組合員マンス]";
				mes "商人になるためには";
				mes "3つの条件が必要だ。";
				next;
				mes "[組合員マンス]";
				mes "1つ、いくら商売主体の商人でも、";
				mes "初心者スキルが9に達して";
				mes "いないと駄目だ。";
				next;
				mes "[組合員マンス]";
				mes "2つ、商人ギルド加入会費";
				mes "1000Zenyは必要だ。商人を目指す";
				mes "者なら、その位のお金を貯めるのは";
				mes "簡単だろう。";
				next;
				mes "[組合員マンス]";
				mes "3つ、基礎体力及び方向感覚のテスト。";
				mes "正確性を試す転職試験だ。";
				mes "指定した人物に会って、";
				mes "荷物を届ける事で商人の資質が";
				mes "あるかどうかを見る。";
				close;
			case 4:
				close;
		}
		//case1の続き
		mes "[組合員マンス]";
		mes "商人になりたいって？ふ…";
		next;
		if(Class != 0) {
			mes "[組合員マンス]";
			mes "我ら商人ギルドの人間は、";
			mes "コウモリみたいな奴が一番嫌いだ。";
			mes "昼は商売して、夜はエッチな服を着て";
			mes "怪しい酒場に通ってるそういう奴。";
			mes "うー…ムカムカ";
			next;
			mes "[組合員マンス]";
		   	mes "まあ、話しが長くなったけど、";
			mes "他の職業を持っている君みたいな奴は";
			mes "商人ギルドには要らない。";
		   	close;
		}
		if(getskilllv(1) < 9 || SkillPoint) {
			mes "[組合員マンス]";
			mes "しかーし！商人になるためには、";
			mes "少なくても初心者JOBスキルが";
			mes "9以上にならなきゃ！";
			mes "いくら金稼ぎの商人でも、";
			mes "最低限、基本がなってないとね。";
			close;
		}
		if(Upper == 1 && OLD_CLASS != 10 && OLD_CLASS != 18) {
			mes "[組合員マンス]";
			mes "あなたはこの職に転職できません。";
			mes "転生前の系列一次職にのみ";
			mes "転職可能です。";
			close;
		}
		if(Upper == 1) {
			mes "[組合員マンス]";
			mes "あなたは、既に転生した身";
			mes "転職試験はいりませんね";
			next;
			unequip;
			jobchange 5,1;
			skill 153,1,0;
			skill 154,1,0;
			skill 155,1,0;
			mes "[組合員マンス]";
			mes "転職おめでとうございます！";
			mes "これからもギルドの";
			mes "復興のため、頑張ってください！";
			close;
		}
		mes "[組合員マンス]";
		mes "よしそれじゃこの申請書にサインして、";
		mes "商人ギルド加入会費1000Zenyを";
		mes "払うのだ。";
		mes "あ、今お金がなかったら、まず";
		mes "500Zenyだけ払ってもらって、残りは";
		mes "試験の後でも良いぞ。どうする？";
		next;
		if(select("もちろん","いや、また今度…")==2) {
			mes "[組合員マンス]";
			mes "今はお金がないか？";
			mes "まあ、仕方がないな";
			mes "焦らずに頑張ってくれ。";
			close;
		}
		mes "[組合員マンス]";
		mes "この申請書は会費と一緒に";
		mes "出さなきゃならないけど…";
		mes "会費の方はどうする？";
		next;
		switch (select("1000Zeny全部払う","先に500Zenyだけ払う","やめます")) {
			case 1:
				if(Zeny < 1000) {
					mes "[組合員マンス]";
					//mes "何でそんなを顔する？";	//本鯖誤字
					mes "何でそんな顔をする？";
					mes "お金がもったいないとか";
					mes "思ってるだろう？";
					close;
				}
				set Zeny,Zeny-1000;
				mes "[組合員マンス]";
				mes "OK… 1000Zeny確かに頂いた。";
				break;
			case 2:
				if(Zeny < 500) {
					mes "[組合員マンス]";
					mes "「1Zenyも持ってない自分が嫌だ」";
					mes "という顔をするな！";
					mes "今からでも遅くないから";
					mes "お金を貯めて来い！";
					close;
				}
				set TEMPVAL_MC,TEMPVAL_MC|1;
				set Zeny,Zeny-500;
				mes "[組合員マンス]";
				mes "ふむ…500Zeny確かに頂いた。";
				mes "まあ、こういう風に分割払いするのは";
				mes "商人として好ましくないが…";
				break;
			case 3:
				mes "[組合員マンス]";
				mes "準備が出来たら、いつでも来い。";
				close;
		}
		set CHANGE_MC,rand(1,8);	//このタイミングでフラグが立つ
		next;
		mes "[組合員マンス]";
		mes "よし、ギルド加入申請書を出す。";
		mes "その次、転職試験について説明する。";
		mes "その前に一言。たまに、人の話しを";
		mes "適当に解釈して、勝手な事をする奴が";
		mes "いるけど、君はそんな事するなよ。";
		next;
		mes "[組合員マンス]";
		break;
	default:	//1〜16のとき
		mes "[組合員マンス]";
		mes "君、またここで何してるの？";
		break;
	case 17:
	case 18:
	case 19:
	case 20:
	case 21:
	case 22:
	case 23:
	case 24:
		mes "あ、" +strcharinfo(0)+ " 、";
		mes "無事に到着したか。まずは領収証を";
		mes "倉庫職員に渡しておいでよ。";
		close;
	case 25:
		mes "[組合員マンス]";
		mes "あ、" +strcharinfo(0)+ " さん、";
		mes "良い知らせだ。";
		mes "君が転職試験を受けている間";
		mes "商人ギルド総会は、君に商人資格と";
		mes "ギルドへの正式入団を承認した。";
		if(TEMPVAL_MC&1) {
			mes "あと残ってるのは会費だけだ。いいか？";
			next;
			if(select("残り500Zenyを払う","やめる")==2) {
				mes "[組合員マンス]";
	   			mes "どうした…？何か問題でもあるのか？";
	 			close;
			}
			if(Zeny < 500) {
				mes "[組合員マンス]";
				mes "ここまで来て勘定ができないとは！";
				mes "商人になろうとする人間は";
				mes "信用第一主義でなければならん！";
				close;
			}
			set Zeny,Zeny-500;
			mes "[組合員マンス]";
			mes "よし！確かに500Zenyもらった。";
		}
		mes "じゃ、話を進めようか！";
		next;
		unequip;
		jobchange 5;
		set CHANGE_MC,0;
		set '@flag,TEMPVAL_MC;	//途中でログアウトされてもいいようにコピー
		set TEMPVAL_MC,0;
		mes "[組合員マンス]";
		mes "おめでとう！";
		mes "組合一同、君が商人ギルドの一員に";
		mes "なった事を心からお祝いする。";
		mes "これからも頑張って活動してくれ。";
		next;
		if('@flag&4) {
			mes "[組合員マンス]";
			mes "(そして…これは小さな金額だけど";
			mes "貰っとけ。手紙を送ってくれた感謝の";
			mes "しるしだ。またよろしく…)";
			set Zeny,Zeny+100;
			next;
		}
		else if('@flag&2) {
   			mes "[組合員マンス]";
			mes "(手紙…忘れてたか…";
			mes "まあ、仕方がないな、ご苦労様)";
   			next;
		}
		mes "[組合員マンス]";
		mes "全世界通貨量の20％確保という";
		mes "我らの壮大な目標を達成するため、";
		mes "情熱溢れる青年の力が必要不可欠だ！";
		mes "判ったか？俺の言う事が!?";
		mes "まあ、今のは話のたとえだ。";
		mes "結論的にはお金を稼ごうって話し。";
		close;
	case 26:
		mes "[組合員マンス]";
		mes "姓名 " +strcharinfo(0)+ ".";
		mes "貴方の商人転職試験は不合格だ。";
		mes "以上。";
		set CHANGE_MC,0;	//最初からやり直し
		set TEMPVAL_MC,0;
		close;
	}
	//16以下のときの続き
	switch(CHANGE_MC) {
		case 1:
		case 2:
		case 9:
		case 10:
			mes "倉庫で荷物を受け取って、プロンテラの";
			mes "元剣士ギルドに行って、";
			mes "(株）カプラ＝プロンテラ支部";
			mes "総責任者に渡せばいい。";
			if(CHANGE_MC == 1 || CHANGE_MC == 9)
				mes "荷物の商品番号は^3355FF2485741^000000だ。";
			else
				mes "荷物の商品番号は^3355FF2328137^000000だ。";
			break;
		case 3:
		case 4:
		case 11:
		case 12:
			mes "倉庫で荷物を受け取って、";
			//mes "ゲフェンの魔法学院の行って";	//本鯖誤字
			mes "ゲフェンの魔法学院に行って";
			mes "そこの関係者に渡せばいい。";
			if(CHANGE_MC == 3 || CHANGE_MC == 11)
				mes "荷物の商品番号は^3355FF2989396^000000だ。";
			else
				mes "荷物の商品番号は^3355FF2191737^000000だ。";
			break;
		case 5:
		case 6:
		case 13:
		case 14:
			mes "倉庫で荷物を受け取って、";
			mes "モロクの染色製作者、";
			mes "<ザバドゥリハン>の見習生に";
			mes "渡せばいい。";
			if(CHANGE_MC == 5 || CHANGE_MC == 13)
				mes "荷物の商品番号は^3355FF3012685^000000だ。";
			else
				mes "荷物の商品番号は^3355FF3487372^000000だ。";
			break;
		case 7:
		case 8:
		case 15:
		case 16:
			mes "倉庫で荷物を受け取って、";
			mes "イズルード近くのバイラン島の";
			mes "カプラ職員に渡せばいい。";
			if(CHANGE_MC == 7 || CHANGE_MC == 15)
				mes "荷物の商品番号は^3355FF3318702^000000だ。";
			else
				mes "荷物の商品番号は^3355FF3543625^000000だ。";
			if(TEMPVAL_MC&2 == 0) {
				next;
				getitem 1072,1;
				set TEMPVAL_MC,TEMPVAL_MC|2;
				mes "[組合員マンス]";
				mes "そして…（これは俺の個人的な";
				mes "頼みなんだけど…荷物を渡すとき";
				mes "この手紙を一緒に渡してくれ。";
				mes "お願いっ！）";
			}
			break;
	}
	next;
	mes "[組合員マンス]";
	if(CHANGE_MC > 8) {
		mes "倉庫は右の部屋だ。";
		if(TEMPVAL_MC&2)
			mes "そして… (手紙もよろしく〜)";
	}
	mes "この番号をよく覚えておいて、";
	mes "右の倉庫で管理者に目的地と";
	mes "商品番号を言うと、荷物がもらえる";
	mes "はずだ。忘れるな！";
	next;
	mes "[組合員マンス]";
	mes "荷物を届け先に届けてから";
	mes "またここに戻って、領収書を";
	mes "倉庫管理者に渡したらOKだ。";
	mes "全部終わったら俺に話し掛けてくれ。";
	mes "それじゃ、気を付けてな！";
	close;
}


//==========================================
// 荷物配達倉庫
//------------------------------------------

alberta_in.gat,28,29,2	script	商人組合員	83,{
	mes "[組合員ケイ]";
	mes "やあー何の用？";
	next;
	switch (select("転職試験のことです","アルバイトの事です","何でもありません")) {
		case 1:
			break;
		case 2:
			mes "[組合員ケイ]";
			mes "アルバイト？まだ準備中だ。";
			mes "総会が予算をくれないんだ。まったく…";
			close;
		case 3:
			mes "[組合員ケイ]";
			mes "…？…うん？うん？";
			mes "何か…？";
			close;
	}
	//case1の続き
	switch(CHANGE_MC) {
	case 0:
		mes "[組合員ケイ]";
		mes "名前は？";
		mes strcharinfo(0)+ " …か。";
		mes "うん？君の名前は名簿にない。";
		mes "もう一度確認しては？";
		close;
	case 1:
	case 2:
	case 3:
	case 4:
	case 5:
	case 6:
	case 7:
	case 8:
		mes "[組合員ケイ]";
		mes "名前は？";
		mes strcharinfo(0)+ " …か。";
		mes "あ、名簿にある。";
		mes "それじゃ荷物を渡すよ。";
		mes "正確な目的地はどこ？";
		break;
	case 9:
	case 10:
	case 11:
	case 12:
	case 13:
	case 14:
	case 15:
	case 16:
		if(countitem(1081) < 1 && countitem(1082) < 1 && countitem(1091) < 1) {
			mes "[組合員ケイ]";
			mes "うん？あー無事に戻って来た？";
			mes "他に用でも？";
			next;
			if(select("荷物を無くしました","何でもありません")==2) {
				mes "[組合員ケイ]";
				mes "用が無いなら早く行ってよ。";
				close;
			}
			mes "[組合員ケイ]";
			//mes "この馬鹿が！荷物をなくしったって？";	//本鯖誤字
			mes "この馬鹿が！荷物をなくしたって？";
			mes "どうするつもりなんだよ！";
			mes "悪いけど規定上、君の記録を";
			mes "商人ギルド転職課に送る。";
			set CHANGE_MC,26;	//試験失敗フラグ
			close;
		}
		mes "[組合員ケイ]";
		mes "うん？まだ出発してなかったのか？";
		mes "何をしてるの？";
		next;
		if(select("荷物を交換してください","何でもないです")==2) {
			mes "[組合員ケイ]";
			mes "…？ なんだよ…。";
			close;
		}
		//アタリ荷物かハズレ荷物かに関わらずインベントリーの若い方の木箱を消す本鯖仕様を再現
		getinventorylist;
		for(set '@i,0; '@i<@inventorylist_count; set '@i,'@i+1) {
			if(@inventorylist_id['@i] == 1081) {
				delitem 1081,1;
				break;
			}
			if(@inventorylist_id['@i] == 1082) {
				delitem 1082,1;
				break;
			}
			if(@inventorylist_id['@i] == 1091) {
				delitem 1091,1;
				break;
			}
		}
		set CHANGE_MC,CHANGE_MC-8;	//戻される
		mes "[組合員ケイ]";
		mes "ったく、馬鹿やろうが！";
		mes "トロくさいんだよ！";
		mes "正確な目的地はどこ？";
		break;
	case 17:
	case 18:
	case 19:
	case 20:
	case 21:
	case 22:
	case 23:
	case 24:
		mes "[組合員ケイ]";
		mes "あ、来たね。じゃ確認するよ。";
		mes "君の名前は…";
		mes strcharinfo(0)+ " か。";
		next;
		mes "[組合員ケイ]";
		mes "目的地は…";
		switch(CHANGE_MC) {
			case 17:
			case 18:
				mes "プロンテラのカプラ姉ちゃんだったな。";
				mes "領収証は？";
				set '@itemid,(CHANGE_MC == 17)? 1073: 1074;
				break;
			case 19:
			case 20:
				mes "ゲフェンの魔法学院だったな。";
				mes "領収証は？";
				set '@itemid,(CHANGE_MC == 19)? 1075: 1076;
				break;
			case 21:
			case 22:
				mes "モロクの染料製作者だったな。";
				mes "領収証は？";
				set '@itemid,(CHANGE_MC == 21)? 1077: 1078;
				break;
			case 23:
			case 24:
				mes "イズルード・バイアラン島の";
				mes "カプラ姉ちゃんだったな。領収証は？";
				set '@itemid,(CHANGE_MC == 23)? 1079: 1080;
				break;
		}
		if(countitem('@itemid)) {
			mes "…よし！完璧だ！";
			mes "君の記録は転職課に送る。";
			mes "中央の<組合員マンス>に話しな！";
			set CHANGE_MC,25;	//試験合格フラグ
			delitem '@itemid,1;
		}
		else {
			mes "うん？何？領収証がない？";
			mes "どうなってるんだ？";
			next;
			mes "君の記録を転職課に送る。";
			mes "中央の<組合員マンス>に話しな！";
			set CHANGE_MC,26;	//試験失敗フラグ
		}
		close;
	case 25:
	case 26:
		mes "[組合員ケイ]";
		mes "君の記録はすでに転職課に送った。";
		mes "中央の<組合員マンス>に話してみて。";
		close;
	}
	//case1〜16の続き
	next;
	set @menu,select("プロンテラ","ゲフェン","モロク","イズルード");
	mes "[組合員ケイ]";
	mes "さあ、荷物の商品番号を書いて。";
	mes "もし、間違ったりしたら、";
	mes "<０>って書けばいい。";
	next;
	while(1) {
		input '@num;
		if('@num == 0) {
			mes "[組合員ケイ]";
			mes "本当にやめるの？";
			if(select("うん","ううん")==1) {
				mes "わかった。取り消しー";
				close;
			}
			continue;
		}
		if('@num < 1000000 || '@num > 5000000) {
			mes "[組合員ケイ]";
			mes "おいおい、多すぎだよ。";
			mes "1000000から5000000";
			mes "までの数字だ。";
			next;
			continue;
		}
		switch(@menu) {
		case 1:
			mes "[組合員ケイ]";
			mes "目的地はプロンテラ！荷物の商品番号は";
			break;
		case 2:
			mes "[組合員ケイ]";
			mes "目的地はゲフェン！うー遠い。";
			mes "君も本当に運がない奴だな。";
			mes "荷物の商品番号は";
			break;
		case 3:
			mes "[組合員ケイ]";
			mes "目的地はモロク！うわー遠いな。";
			mes "君も本当に運がない奴だな。";
			mes "荷物の商品番号は";
			break;
		case 4:
			mes "[組合員ケイ]";
			mes "目的地はバイアラン島！";
			mes "荷物の商品番号は";
			break;
		}
		mes '@num+ " だ。OK？";
		next;
		if(select("OK.","NO.")==2)
			continue;
		setarray '@code,2485741,2328137,2989396,2191737,3012685,3487372,3318702,3543625;
		if('@num == '@code[CHANGE_MC-1]) {
			if(CHANGE_MC%2)		//フラグが奇数なら
				getitem 1081,1;
			else			//フラグが偶数なら
				getitem 1082,1;
		}
		else
			getitem 1091,1;		//ハズレ荷物
		set CHANGE_MC,CHANGE_MC+8;
		mes "[組合員ケイ]";
		mes "1回支給された荷物を失くしたら、";
		mes "転職は無しだ。絶対無くすなよ！";
		mes "それじゃ、行っていらっしゃい。";
		mes "あ、領収証も忘れるな！じゃ！";
		close;
	}
}


//==========================================
// 配達
//------------------------------------------

prontera.gat,248,42,0	script	カプラ職員::MC_DeliverPro	116,{
	cutin "kafra_02",2;
	switch(CHANGE_MC) {
	default:
		mes "[カプラ職員]";
		mes "いらっしゃいませ、（株）カプラです。";
		mes "ご覧のとおり剣士ギルドは";
		mes "首都プロンテラから";
		mes "衛星都市「イズルード」に";
		mes "移転しました。";
		next;
		mes "[カプラ職員]";
		mes "我々（株）カプラでは";
		mes "ここプロンテラからイズルードまで";
		mes "利用料 600 Zenyで移動サービスを";
		mes "行っています。";
		next;
		switch (select("利用する","カプラポイント確認","終了")) {
			case 1:
				if(Zeny<600) {
					mes "[カプラ職員]";
					mes "お客様、お金が足りないようですが。";
					break;
				}
				set Zeny,Zeny-600;
				set KAFRA_PIT,KAFRA_PIT+60;
				warp "izlude.gat",94,103;
				end;
			case 2:	//function内からコピー
				mes "[カプラ職員]";
				mes strcharinfo(0)+ "様の獲得ポイントは、";
				mes KAFRA_PIT+ "点です。";
				next;
				mes "[カプラ職員]";
				mes "カプラポイントは当カプラサービスの";
				mes "本社で賞品と引き換えが可能です。";
				mes "今後ともカプラサービスをよろしく";
				mes "お願い致します。";
				break;
			case 3:
				mes "[カプラ職員]";
				mes "ありがとうございました。";
				break;
		}
		break;
	case 1:
	case 2:
		mes "[カプラ職員]";
		mes "こんにちは";
		mes "ここに何かご用ですか？";
		break;
	case 9:
	case 10:
		mes "[カプラ職員]";
		mes "商人ギルドから荷物を届けに？";
		mes "あ、はい！では荷物はここに…";
		set '@id,(CHANGE_MC == 9)? 1081: 1082;
		if(countitem('@id) > 1 || (countitem(1081) < 1 && countitem(1082) < 1 && countitem(1091) < 1)) {	//1個かどうか判定！
			mes "あの…品物はどこですか…？";
			break;
		}
		mes "こんなに重いものを…";
		mes "ここまでご苦労様でした。";
		next;
		mes "[カプラ職員]";
		mes "品物の伝票番号を確認してみます。";
		mes "えっと…";
		if(CHANGE_MC == 9 && countitem(1081)) {
			delitem 1081,1;
			getitem 1073,1;
			set CHANGE_MC,17;
			mes "2485741… はい。";
		}
		else if(CHANGE_MC == 10 && countitem(1082)) {
			delitem 1082,1;
			getitem 1074,1;
			set CHANGE_MC,18;
			mes "2328137… はい。";
		}
		else {
			mes "……え？";
			mes "この番号は……？";
			next;
			mes "[カプラ職員]";
			mes "あの、この品物は";
			mes "こちらのものではございません。";
			mes "商人ギルドからここに来る品物の";
			mes "伝票番号は2485741又は2328137です。";
			mes "何かの間違いではないでしょうか？";
			break;
		}
		mes "確かに注文した品物です。";
		mes "配達領収書が必要ですね？";
		mes "すぐ発行致します。";
		next;
		mes "[カプラ職員]";
		mes "こんなに遠くまでご苦労さまでした。";
		break;
	}
	close2;
	cutin "kafra_02",255;
	end;
OnInit:
	//これが読み込まれるときはnpc_town_kafra.txtの方を無効にする
	disablenpc "カプラ職員#ProSword";
	end;
}

//==============================================================
geffen_in.gat,155,122,4	script	ギルド関係者	47,{
	switch(CHANGE_MC) {
	default:
		mes "[魔法ギルド関係者]";
		mes "荷物が届く頃なのに…";
		mes "遅いな…ふん…";
		close;
	case 11:
	case 12:
		mes "[魔法ギルド関係者]";
		mes "あ、来た来た！";
		mes "商人ギルドからでしょう？";
		mes "さあ、こちらへ…。";
		set '@id,(CHANGE_MC == 11)? 1081: 1082;
		if(countitem('@id) > 1 || (countitem(1081) < 1 && countitem(1082) < 1 && countitem(1091) < 1)) {	//1個かどうか判定！
			mes "あらら？荷物は？荷物はどこですか？";
			close;
		}
		mes "こんなに熱い中、本当にお疲れ様〜";
		next;
		mes "[魔法ギルド関係者]";
		mes "品物の伝票番号を確認してみます。";
		mes "えっと…";
		if(CHANGE_MC == 11 && countitem(1081)) {
			delitem 1081,1;
			getitem 1075,1;
			set CHANGE_MC,19;
			mes "2989396…はい、OKです。";
		}
		else if(CHANGE_MC == 12 && countitem(1082)) {
			delitem 1082,1;
			getitem 1076,1;
			set CHANGE_MC,20;
			mes "2191737…はい、OKです。";
		}
		else {
			mes "…？ 番号が違う!?";
			next;
			mes "[魔法ギルド関係者]";
			mes "えーと、この品物は";
			mes "こちらのものじゃないですね。";
			mes "商人ギルドからここに来る品物の";
			mes "伝票番号は^3355FF2989396^000000";
			mes "または^3355FF2191737^000000です。";
			mes "受け取る時に間違えちゃいましたか？";
			close;
		}
		mes "さあ、領収証を。";
		next;
		mes "[魔法ギルド関係者]";
		mes "ふふ、今後もよろしく〜";
		close;
	case 19:
	case 20:
	case 25:
		mes "[魔法ギルド関係者]";
		mes "やー商人ギルドの方、元気ですか？";
		mes "また、よろしくー。";
		close;
	}
}

//==============================================================
morocc_in.gat,140,102,4	script	見習生	86,{
	switch(CHANGE_MC) {
	default:
		mes "[染料製作の見習い]";
		mes "ザバドゥリハン先生の染色の腕は、";
		mes "ミッドガルド大陸の誰も真似できない！";
		mes "素晴らしい腕の持ち主ですよ。";
		mes "僕はあの方の弟子です！";
		close;
	case 13:
	case 14:
		mes "[染料製作の見習い]";
		mes "商人ギルドから？";
		mes "あ、お疲れ様です〜。";
		set '@id,(CHANGE_MC == 13)? 1081: 1082;
		if(countitem('@id) > 1 || (countitem(1081) < 1 && countitem(1082) < 1 && countitem(1091) < 1)) {	//1個かどうか判定！
			mes "…で、荷物はどこですか？";
			close;
		}
		mes "じゃ、ここへお願いします。";
		next;
		mes "[染料製作の見習い]";
		mes "領収証を渡す前に、ちょっと";
		mes "荷物の商品番号を確認しますね。";
		if(CHANGE_MC == 13 && countitem(1081)) {
			delitem 1081,1;
			getitem 1077,1;
			set CHANGE_MC,21;
			mes "3012685…注文した物に";
		}
		else if(CHANGE_MC == 14 && countitem(1082)) {
			delitem 1082,1;
			getitem 1078,1;
			set CHANGE_MC,22;
			mes "3487372…注文した物に";
		}
		else {
			next;
			mes "[染料製作の見習い]";
			mes "…あの、うちの荷物じゃないみたい";
			mes "みたいですけど…来るはずの商品番号は";
			mes "^3355FF3012685^000000または^3355FF3487372^000000";
			mes "です…。";
			close;
		}
		mes "間違いありません。はい、領収証です。";
		next;
		mes "[染料製作の見習い]";
		mes "またよろしくお願いします〜。";
		close;
	case 21:
	case 22:
	case 25:
		mes "[染料製作の見習い]";
   		mes "あ、お帰りになりますか？さよなら〜";
   		close;
	}
}

//==============================================================
izlu2dun.gat,106,58,4	script	カプラ職員::MC_DeliverBya	116,{
	cutin "kafra_02",2;
	switch(CHANGE_MC) {
	default:
		callfunc "KafraMain",0,0x5a,0,0,0,0,0,0,40,800;
		end;
	case 7:
	case 8:
		mes "[カプラ職員]";
		mes "はい、何のご用ですか？";
		break;
	case 15:
	case 16:
		mes "[カプラ職員]";
		mes "え？商人ギルドからいらしたんですか？ ";
		mes "……あ！そうだ！わたしったら〜 ";
		mes "じゃ、品物はここに…";
		set '@id,(CHANGE_MC == 15)? 1081: 1082;
		if(countitem('@id) > 1 || (countitem(1081) < 1 && countitem(1082) < 1 && countitem(1091) < 1)) {	//1個かどうか判定！
			mes "ここに配達される品物は";
			mes "箱1個のはずですが…。";
			break;
		}
		mes "はい、おつかれさま〜。";
		next;
		mes "[カプラ職員]";
		mes "領収書発行のため";
		mes "品物の伝票番号を確認してみます。";
		if(CHANGE_MC == 15 && countitem(1081)) {
			delitem 1081,1;
			getitem 1079,1;
			set CHANGE_MC,23;
			mes "えっと…3318702…はい！";
		}
		else if(CHANGE_MC == 16 && countitem(1082)) {
			delitem 1082,1;
			getitem 1080,1;
			set CHANGE_MC,24;
			mes "えっと…3543625…はい！";
		}
		else {
			mes "[カプラ職員]";
			mes "……？…あの…";
			mes "配達先を間違えてるみたいですよ？";
			mes "商人ギルドから、ここに来る品物の";
			mes "伝票番号は3318702又は3543625ですが…";
			break;
		}
		mes "確かに注文した品物ですね。";
		mes "では、こちらが領収書です。";
		next;
		if(TEMPVAL_MC&2 && countitem(1072)) {
			menu "これは商人組合のマンスさんから……",-;
			mes "[カプラ職員]";
			mes "あら！マンスさんからの手紙？キャ〜";
			mes "商人ギルドの職員様！ありがとう〜";
			mes "すぐ手紙が来るとは思っていたけど";
			mes "こんなに早いなんて。";
			delitem 1072,1;
			set TEMPVAL_MC,TEMPVAL_MC|4;
			next;
		}
		mes "[カプラ職員]";
		mes "こんなに遠い所まで、ご苦労様でした。";
		mes "つまらない物ですが、どうぞ！";
		mes "では、これからも";
		mes "よろしくお願いします。";
		getitem (rand(2))? 512: 513,3;
		break;
	}
	close2;
	cutin "kafra_02",255;
	end;
OnInit:
	//これが読み込まれるときはnpc_town_kafra.txtの方を無効にする
	disablenpc "カプラ職員#Byalan";
	end;
}