//====================================================================
//Ragnarok Online Gunslinger Jobchange Script
//　■ CHANGE_GS -> 0〜
//====================================================================

//==========================================
// ショップ
//------------------------------------------
que_ng.gat,179,91,3	shop	武器商人・シバス	900,13150:3000,13102:20000,13151:17500,13154:12000,13155:32000
que_ng.gat,180,79,3	shop	武器商人・ウィコ	900,13200:1,13201:15,13202:30

//==========================================
// 試験申請
//------------------------------------------
que_ng.gat,152,167,3	script	マスターミラー	901,{
	if(Class == 28) {
		mes "[マスターミラー]";
		mes "久しぶりだな。";
		mes "銃は大切に扱うんだぞ。";
		close;
	}
	if(Class != 0 || Upper != 0) {
		mes "[マスターミラー]";
		mes "私と目を合わせないでくれ。";
		mes "ここはお店じゃないんだ。";
		close;
	}
	switch(CHANGE_GS) {
	case 0:
		if(getskilllv(1) < 9) {
			mes "[マスターミラー]";
			mes "ふむ……";
			mes "私の目が確かなら、";
			mes "君は十分な可能性を秘めている。";
			mes "更なる努力を重ね、";
			mes "基本的な技術を磨いたら";
			mes "もう一度来るといい。";
			close;
		}
		mes "[マスターミラー]";
		mes "私はセルレナお嬢さんの";
		mes "警護主任であり、";
		mes "ガンスリンガー訓練教官でもある";
		mes "マスターミラー。";
		next;
		mes "[マスターミラー]";
		mes "一時も無駄にできないこの私に";
		mes "声を掛けた理由は何だね？";
		next;
		if(select("べつに……","ガンスリンガーになりたいです")==1) {
			mes "[マスターミラー]";
			mes "私の貴重な時間を";
			mes "無駄にさせるとは……";
			mes "今すぐ私の前から消えろ。";
			close;
		}
		mes "[マスターミラー]";
		mes "ほほぅ……";
		mes "まだ幼いが、";
		mes "なかなか良い目をしている。";
		next;
		mes "[マスターミラー]";
		mes "ふむ……よかろう。";
		mes "本気でガンスリンガーになりたいなら";
		mes "まず簡単な手続きが必要。";
		mes "その後、面接と教育がある。";
		next;
		mes "[マスターミラー]";
		mes "その覚悟はあるかね？";
		next;
		if(select("少し考えてみます","あります")==1) {
			mes "[マスターミラー]";
			mes "む……そうか。";
			mes "だが、この職業は";
			mes "慎重な者でなければ勤まらん。";
			mes "決心がついてから";
			mes "また来るといい。";
			close;
		}
		mes "[マスターミラー]";
		mes "よかろう。";
		mes "では、この手紙を持って";
		mes "フェイヨンにいらっしゃる";
		mes "ファンソプル様を";
		mes "訪ねなさい。";
		next;
		mes "[マスターミラー]";
		mes "その方が君を、";
		mes "ガンスリンガーとして";
		mes "相応しい人間なのか";
		mes "判断してくださる。";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "はい！　わかりました。";
		set CHANGE_GS,1;
		close;
	case 1:
		mes "[マスターミラー]";
		mes "はやく行きなさい。";
		mes "ファンソプル様は";
		mes "フェイヨンにいらっしゃる。";
		close;
	case 2:
		mes "[マスターミラー]";
		mes "ファンソプル様が";
		mes "印の材料と？";
		mes "…………";
		next;
		mes "[マスターミラー]";
		mes "どうやらファンソプル様は、";
		mes "君の事が気に入られたようだな。";
		close;
	case 3:
		mes "[マスターミラー]";
		mes "頼まれごとをされたのか？";
		mes "ファンソプル様によっぽど";
		mes "気に入られたらしいな。";
		close;
	case 4:
		mes "[マスターミラー]";
		mes "君には期待している。";
		mes "早くファンソプル様の元へ";
		mes "行くといい。";
		close;
	case 5:
		mes "[マスターミラー]";
		mes "ファンソプル様から";
		mes "印を受け取ってきたな。";
		mes "……本当に久しぶりだ。";
		mes "普通はそう簡単には";
		mes "印はもらえないのだがな。";
		next;
		mes "[マスターミラー]";
		mes "よし、いいだろう。";
		mes "ファンソプル様に許されたなら";
		mes "私が拒否する理由は無い。";
		next;
		mes "[マスターミラー]";
		mes "では、私は君を";
		mes "ガンスリンガーとして任命する。";
		mes "だがその前に、";
		mes "ガンスリンガーについて";
		mes "重要な説明をしよう。";
		next;
		mes "[マスターミラー]";
		mes "武器と実弾の購入は、";
		mes "各地にいるガンスリンガーギルドの";
		mes "関係者を通して行う。";
		next;
		mes "[マスターミラー]";
		mes "面倒だとは思うだろうが、";
		mes "この武器を悪用されないため、";
		mes "悪人の手に渡らないための処置だ。";
		mes "理解してくれ。";
		next;
		mes "[マスターミラー]";
		mes "他にも様々な理由がある。";
		mes "だが、これらの規定は全て、";
		mes "何時か君もお会いするだろう、";
		mes "このギルドの責任者である";
		mes "セルレナお嬢様の指示だ。";
		next;
		mes "[マスターミラー]";
		mes "時がくれば、";
		mes "君に与えられる任務とともに";
		mes "お嬢様の意思が理解できるだろう。";
		next;
		mes "[マスターミラー]";
		mes "では、私はそろそろ別の用があるので。";
		mes "大地の加護が君と共にあるのを祈り、";
		mes "この武器を君に授けよう。";
		unequip;
		jobchange 28;
		set CHANGE_GS,0;
		getitem 13100,1;
		close;
	}
}

//==========================================
// 転職試験
//------------------------------------------
payon.gat,184,65,3	script	ファンソプル	866,{
	if(Class == 28) {
		mes "[ファンソプル]";
		mes "うむ……久しぶりじゃの。";
		mes "賢い野獣になるのじゃぞ。";
		close;
	}
	switch(CHANGE_GS) {
	case 0:
		mes "[ファンソプル]";
		mes "ぐぅ……ぐぅぐぅ……";
		close;
	case 1:
		mes "[ファンソプル]";
		mes "……ん……";
		mes "……若いオオカミが来おった……";
		mes "ワシに何の用かな？";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "ミラーさんの紹介で";
		mes "ここに来ました。";
		next;
		mes "[ファンソプル]";
		mes "ミラー……";
		mes "……あぁ、黒い狐の";
		mes "紹介で来たのかね……";
		next;
		mes "[ファンソプル]";
		mes "彼はいい人だ……";
		mes "仲間のためなら命も";
		mes "かけることができる、";
		mes "正義の心を持っている。";
		next;
		mes "[ファンソプル]";
		mes "彼の紹介なら、";
		mes "何の用か大体わかる……";
		next;
		mes "[ファンソプル]";
		mes "さぁ……もう少し";
		mes "こっちに来なさい……";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "…………";
		next;
		mes "[ファンソプル]";
		mes "ん……";
		mes "うんうん……";
		next;
		mes "[ファンソプル]";
		mes "……清らかな目と";
		mes "善良な心……そして";
		mes "大事なものを守ることができる";
		mes "責任感を持っている……";
		mes "しかし……幼い……";
		next;
		mes "[ファンソプル]";
		mes "まだ経験が不足している……";
		mes "荒野と大地の祝福を";
		mes "受けることができん……";
		mes "だから、お主の代わりとなる";
		mes "大地の印を作ってやろう。";
		next;
		mes "[ファンソプル]";
		mes "華麗な蟲の皮3個、木屑1個、";
		mes "かたい皮10個、やわらかな毛3個";
		mes "ジャルゴン3個、緑ハーブ3個";
		mes "を持って来るのだ。";
		next;
		mes "[ファンソプル]";
		mes "それらで作った大地の印を";
		mes "黒い狐に見せれば、";
		mes "お主の願いはかなうだろう。";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "-華麗な蟲の皮3個、木屑1個、";
		mes "かたい皮10個、やわらかな毛3個";
		mes "ジャルゴン3個、緑ハーブ3個";
		mes "を取りに行こう-";
		set CHANGE_GS,2;
		close;
	case 2:
		if(countitem(1013) < 3 || countitem(1019) < 1 || countitem(935) < 10 || countitem(949) < 3 || countitem(912) < 3 || countitem(511) < 3) {
			mes "[" +strcharinfo(0)+ "]";
			mes "-華麗な蟲の皮3個、木屑1個、";
			mes "かたい皮10個、やわらかな毛3個";
			mes "ジャルゴン3個、緑ハーブ3個";
			mes "を取りに行こう-";
			close;
		}
		mes "[ファンソプル]";
		mes "ん……よく持ってきたな。";
		mes "印を作るにはしばしの時間が";
		mes "必要じゃ。";
		mes "しばらく待っておれ。";
		set CHANGE_GS,3;
		delitem 1013,3;
		delitem 1019,1;
		delitem 935,10;
		delitem 949,3;
		delitem 912,3;
		delitem 511,3;
		close;
	case 3:
		mes "[ファンソプル]";
		mes "ん……ちょうどいい。";
		mes "今、準備ができたところじゃ。";
		mes "単純な印ではあるが……";
		mes "久しぶりじゃ……";
		next;
		mes "[ファンソプル]";
		mes "印を作り始めて、";
		mes "もう数十年になるか……";
		mes "ワシにも大地の戦士として、";
		mes "このような印をつけて";
		mes "戦った時があった……";
		next;
		mes "[ファンソプル]";
		mes "セルレナの父に出会ったのが";
		mes "ついこの間のようじゃ……";
		mes "本当に……時間というやつは";
		mes "空に吹く風のように過ぎ去る……";
		next;
		mes "[ファンソプル]";
		mes "色々な事があったが……";
		mes "ワシが守ってきた意思を";
		mes "受け継ぎ、大切にしてくれる";
		mes "セルレナと黒い狐。";
		mes "そして多くの若者達に";
		mes "感謝している……";
		next;
		mes "[ファンソプル]";
		mes "ワシは老いて、もう力が無い。";
		mes "やがて大地の元へ";
		mes "帰る日が来るであろう……";
		next;
		mes "[ファンソプル]";
		mes "……ふぅ。";
		mes "急に疲れがきおった。";
		mes "悪いが、最後に一つだけ";
		mes "頼みがあるんじゃが……";
		next;
		mes "[ファンソプル]";
		mes "のどが渇いての……";
		mes "冷たいミルクを一杯飲みたい。";
		next;
		mes "[ファンソプル]";
		mes "おいぼれの頼みを";
		mes "聞いてもらえんかの。";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "-お爺さんのために";
		mes "ミルクを一本買ってこよう-";
		set CHANGE_GS,4;
		close;
	case 4:
		if(countitem(519) < 1) {
			mes "[" +strcharinfo(0)+ "]";
			mes "-お爺さんのために";
			mes "ミルクを一本買ってこよう-";
			close;
		}
		set CHANGE_GS,5;
		delitem 519,1;
		mes "[ファンソプル]";
		mes "ありがとう。";
		mes "本当に心がきれいな若者じゃな。";
		next;
		mes "[ファンソプル]";
		mes "ほれ。";
		mes "これがワシの作った印じゃ。";
		mes "これを黒い狐に見せれば";
		mes "ガンスリンガーになれるじゃろう。";
		next;
	case 5:
		mes "[ファンソプル]";
		mes "んん〜〜ん〜ふ〜ん〜らら〜";
		mes "ららる〜ん〜んん〜〜〜";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "-お爺さんは、突然";
		mes "歌を口ずさんだ-";
		mes "-歌詞も無いのに、";
		mes "なんだか不思議な気持ちになる-";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "-さぁ、受けとった印を";
		mes "ミラーさんに見せに行こう-";
		close;
	}
}

//==========================================
// 弾丸屋
//------------------------------------------
que_ng.gat,187,149,3	script	弾丸屋・トニー	86,{
	if(Class != 28) {
		mes "[トニー]";
		mes "俺はガンスリンガーに";
		mes "アイテムを支給している";
		mes "弾丸屋のトニー。";
		next;
		mes "[トニー]";
		mes "お前はガンスリンガーじゃないから";
		mes "適当に見物でもしててくれ。";
		close;
	}
	mes "[トニー]";
	mes "俺は弾丸屋のトニー！";
	mes "弾が不足しているなら";
	mes "いつでもきな！";
	next;
	mes "[トニー]";
	mes "さぁ、我が友、我が同士！";
	mes "何が不足しているんだ!?";
	next;
	switch(select("ポイズンスフィア","ファイアスフィア","ウィンドスフィア","ダークスフィア","アイススフィア","やめる")) {
	case 1:	//ポイズンスフィア - 毒の牙10
		set '@itemid,13205;
		set '@need,937;
		set '@amount,10;
		break;
	case 2:	//ファイアスフィア - 燃えている心臓2
		set '@itemid,13203;
		set '@need,7097;
		set '@amount,2;
		break;
	case 3:	//ウィンドスフィア - サイファー3
		set '@itemid,13204;
		set '@need,7053;
		set '@amount,3;
		break;
	case 4:	//ダークスフィア - 墨汁5
		set '@itemid,13206;
		set '@need,1024;
		set '@amount,5;
		break;
	case 5:	//アイススフィア - ブリガン2
		set '@itemid,13207;
		set '@need,7054;
		set '@amount,2;
		break;
	case 6:
		mes "[トニー]";
		mes "そうか。";
		mes "また来てくれよ。";
		mes "弾丸屋のトニーは";
		mes "いつもここにいるぜ！";
		close;
	}
	mes "[トニー]";
	mes "プラコン1個、";
	mes "エンベルタコン1個、";
	mes getitemname('@need)+'@amount+ "個で";
	mes getitemname('@itemid)+ "30個を";
	mes "1セットとして交換してるぜ。";
	next;
	mes "[トニー]";
	mes "何セット欲しいんだ？";
	mes "一度に500セットまで";
	mes "取引できる。";
	mes "取引をやめるなら0でいいぜ。";
	next;
	input '@num;
	if('@num <= 0 || '@num > 500) {
		mes "[トニー]";
		mes "OK、取引中止だ。";
		close;
	}
	if(countitem(1010) < 1*'@num || countitem(1011) < 1*'@num || countitem('@need) < '@amount*'@num) {
		mes "[トニー]";
		mes "おいおい。";
		mes "材料が足りないんじゃないか？";
		mes "ちゃんと確認してから";
		mes "取引しようぜ。";
		close;
	}
	delitem 1010,'@num;
	delitem 1011,'@num;
	delitem '@need,'@amount*'@num;
	getitem '@itemid,30*'@num;
	mes "[トニー]";
	mes "よし、取引完了！";
	mes "また来てくれよ！";
	close;
}

//==========================================
// 弾丸ケース屋
//------------------------------------------
que_ng.gat,187,149,3	script	弾丸ケース屋・ケニー	83,{
	if(Class != 28) {
		mes "[ケニー]";
		mes "私はガンスリンガーの方に";
		mes "弾丸と弾丸ケースを交換している";
		mes "ケニーと言います。";
		next;
		mes "[ケニー]";
		mes "あなたはガンスリンガーではないので";
		mes "弾丸を扱えません。";
		mes "申し訳ありません。";
		close;
	}
	mes "[ケニー]";
	mes "私は弾丸ケース屋のケニー！";
	mes "弾薬が重いのなら";
	mes "是非ご利用ください。";
	next;
	mes "[ケニー]";
	mes "私が作った弾丸ケースは";
	mes "弾丸を気楽に持ち歩けるように";
	mes "できる優れものです。";
	next;
	switch(select("ウィンドスフィアケース","ダークスフィアケース","ポイズンスフィアケース","アイススフィアケース","ファイアスフィアケース","バレットケース","ブラッドバレットケース","シルバーバレットケース","やめる")) {
	case 1:	//ウィンドスフィアケース
		set '@itemid,13204;
		break;
	case 2:	//ダークスフィアケース
		set '@itemid,13206;
		break;
	case 3:	//ポイズンスフィアケース
		set '@itemid,13205;
		break;
	case 4:	//アイススフィアケース
		set '@itemid,13207;
		break;
	case 5:	//ファイアスフィアケース
		set '@itemid,13203;
		break;
	case 6:	//バレットケース
		set '@itemid,13200;
		break;
	case 7:	//ブラッドバレットケース
		set '@itemid,13202;
		break;
	case 8:	//シルバーバレットケース
		set '@itemid,13201;
		break;
	case 9:
		mes "[ケニー]";
		mes "必要になったら";
		mes "また来てください。";
		close;
	}
	mes "[ケニー]";
	mes getitemname(12143+@menu)+ "は、";
	mes getitemname('@itemid)+ "500発と";
	mes "手数料500Zenyで";
	mes "1個交換できます。";
	next;
	mes "[ケニー]";
	mes "購入する数を入力してください。";
	mes "一度に50個まで交換できます。";
	mes "交換を止めるなら";
	mes "0を入力してください。";
	next;
	input '@num;
	if('@num <= 0 || '@num > 50) {
		mes "[ケニー]";
		mes "交換を中断しました。";
		mes "また来てください。";
		close;
	}
	if(countitem('@itemid) < 500*'@num) {
		mes "[ケニー]";
		mes "アイテムが足りないみたいですよ。";
		mes "一度確認してみてください。";
		close;
	}
	if(Zeny < 500*'@num) {
		mes "[ケニー]";
		mes "お金が足りないみたいですよ。";
		mes "手数料は" +(500*'@num)+ "Zenyです。";
		mes "一度確認してみてください。";
		close;
	}
	set Zeny,Zeny-500*'@num;
	delitem '@itemid,500*'@num;
	getitem 12143+@menu,'@num;
	mes "[ケニー]";
	mes "取引完了です。";
	mes "またご利用ください。";
	close;
}

//==========================================
// ガリスン製作
//------------------------------------------
que_ng.gat,182,85,3	script	ガリスン	109,{
	if(Class != 28) {
		mes "[ガリスン]";
		mes "あなたはガンスリンガーでは";
		mes "ありませんね。";
		mes "私に用は無いと思うのですが？";
		close;
	}
	mes "[ガリスン]";
	mes "私の名はガリスン。";
	mes "そして、私の製作した銃は";
	mes "私のように完璧なので";
	mes "同じくガリスンと言います。";
	next;
	mes "[ガリスン]";
	mes "私にガリスンの";
	mes "製作依頼をしますかな？";
	next;
	mes "[ガリスン]";
	mes "ガリスンを作るには";
	mes "鋼鉄50個、エルニウム3個、";
	mes "オリデオコン1個、石炭50個、";
	mes "錆びたネジ20個と";
	mes "3万Zenyが必要です。";
	next;
	switch(select("また今度で","今すぐ作ってください","やめる")) {
	case 1:
		mes "[ガリスン]";
		mes "そうですか。";
		mes "では、良く考えてから";
		mes "お返事をください。";
		close;
	case 2:
		if(countitem(999) < 50 || countitem(985) < 3 || countitem(984) < 1 || countitem(1003) < 50 || countitem(7317) < 20) {
			mes "[ガリスン]";
			mes "ガリスンを作るには";
			mes "鋼鉄50個、エルニウム3個、";
			mes "オリデオコン1個、石炭50個、";
			mes "錆びたネジ20個と";
			mes "3万Zenyが必要です。";
			mes "お忘れにならないよう。";
			close;
		}
		if(Zeny < 30000) {
			mes "[ガリスン]";
			mes "お金が足りません。";
			close;
		}
		if(checkweight(13104,1) == 0) {
			mes "[ガリスン]";
			mes "荷物が一杯ですね。";
			mes "一度整理してから";
			mes "またお越しください。";
			close;
		}
		delitem 999,50;
		delitem 985,3;
		delitem 984,1;
		delitem 1003,50;
		delitem 7317,20;
		set Zeny,Zeny-30000;
		getitem 13104,1;
		mes "[ガリスン]";
		mes "ではどうぞ。";
		mes "こちらがガリスンです。";
		mes "また必要になったら";
		mes "お越しください。";
		close;
	case 3:
		mes "[ガリスン]";
		mes "それでは、またお会いしましょう。";
		close;
	}
}

//==========================================
// ブッチャー製作
//------------------------------------------
que_ng.gat,149,178,4	script	研究所企画者	744,{
	mes "[エイ]";
	mes "あ、こんにちは。";
	mes "私は、アインブロック研究所企画者、";
	mes "エイといいます。";
	next;
	menu "話しをする",-;
	if(Class != 28) {
		mes "[エイ]";
		mes "もし、知り合いに";
		mes "ガンスリンガーの人がいたら、";
		mes "ある武器に関する情報があるので";
		mes "ここに来るようにと";
		mes "伝えてください。";
		close;
	}
	mes "[エイ]";
	mes "……連射……";
	mes "持続……";
	mes "……180以上……";
	next;
	mes "[エイ]";
	mes "あ、すみません。";
	mes "今は武器の研究で忙しいので。";
	close;
}

//==========================================
// デストロイヤー製作
//------------------------------------------
que_ng.gat,185,180,3	script	ベネッサ	726,{
	mes "[ベネッサ]";
	mes "は！　いやぁ！";
	mes "せい！　とりゃぁ！";
	mes "いつか必ず";
	mes "世界中の格闘技を";
	mes "全部マスターするぞ！";
	next;
	if(Class != 28) {
		mes "[ベネッサ]";
		mes "ん？　何？";
		mes "練習の邪魔になるから";
		mes "あっち行ってくれる？";
		close;
	}
	mes "[ベネッサ]";
	mes "ん？";
	mes "あんたガンスリンガーだね？";
	mes "練習の邪魔になるんだけど。";
	mes "何か用なの？";
	next;
	mes "[" +strcharinfo(0)+ "]";
	mes "いえ、練習を見ていました。";
	mes "すごいかっこよかったから……";
	next;
	mes "[ベネッサ]";
	mes "お！";
	mes "あんた格闘技が好きなの？";
	mes "ならこっちへ来なよ。";
	mes "私が技をかけてやるよ。";
	next;
	mes "[" +strcharinfo(0)+ "]";
	mes "……え？";
	mes "あ、いや、";
	mes "お気になさらずに。";
	next;
	mes "[ベネッサ]";
	mes "大丈夫、大丈夫。";
	mes "ほら！";
	mes "-シュ！　シュ！-";
	next;
	mes "[" +strcharinfo(0)+ "]";
	mes "あわわ、こっちに来ないでください！";
	mes "わ……うわぁ！";
	mes "-シュッシュ！-";
	mes "-ぶん！　ぶぉん！-";
	next;
	mes "[ベネッサ]";
	mes "何で避けるのよ！";
	next;
	mes "[" +strcharinfo(0)+ "]";
	mes "当たり前じゃないですか！";
	mes "危ないでしょうが！";
	next;
	mes "[ベネッサ]";
	mes "やれやれ。";
	mes "これぐらいで";
	mes "びびらないでよ。";
	next;
	mes "[" +strcharinfo(0)+ "]";
	mes "わかっているんですか！";
	mes "当たったらケガをするんですよ！";
	next;
	mes "[ベネッサ]";
	mes "はいはい、そうねー";
	next;
	mes "[" +strcharinfo(0)+ "]";
	mes "大体いきなり…………";
	next;
	mes "[ベネッサ]";
	mes "シャラップ！";
	next;
	mes "[" +strcharinfo(0)+ "]";
	mes "………………";
	next;
	mes "[ベネッサ]";
	mes "はい。";
	mes "確かにやりすぎたわね。";
	mes "一応反省してるのよ。";
	mes "だから、特別にあんたのために";
	mes "武器を作ってあげるわよ。";
	next;
	mes "[ベネッサ]";
	mes "私はベネッサ・ルイス。";
	mes "元々は格闘志望生だったんだけど、";
	mes "今はここで武器を作っているわ。";
	mes "ま、お金が無いってことよ。";
	next;
	mes "[ベネッサ]";
	mes "私が作れる武器は";
	mes "デストロイヤー。";
	next;
	mes "[ベネッサ]";
	mes "私が格闘技の大会に出る時に";
	mes "使う名前を付けた武器よ。";
	mes "ふふふ……";
	mes "デストロイヤー！";
	next;
	mes "[" +strcharinfo(0)+ "]";
	mes "わあ！";
	mes "もうやめてくださいって！";
	next;
	mes "[ベネッサ]";
	mes "わかってる、わかってるって。";
	mes "もー";
	mes "一応、デストロイヤーを作るには";
	mes "材料が必要よ。";
	next;
	mes "[ベネッサ]";
	mes "どう？";
	mes "こんな機会はそうそうないわよ。";
	mes "作ってみる？";
	next;
	if(select("またにします","お願いします")==1) {
		mes "[ベネッサ]";
		mes "あらら、残念。";
		mes "あんたには良い機会だったのに。";
		mes "後で後悔しても知らないよ。";
		mes "じゃ、ばいば〜い。";
		close;
	}
	mes "[ベネッサ]";
	mes "オッケー。";
	mes "デストロイヤーを作るには";
	mes "古い鉄板50個、";
	mes "オリデオコン5個、";
	mes "錆びたネジ70個、";
	mes "手数料の10万Zenyが必要よ。";
	close;
}

//==========================================
// インフェルノ製作
//------------------------------------------
que_ng.gat,187,163,3	script	イングリッド	744,{
	mes "[イングリッド]";
	mes "こんにちは！";
	mes "武器の製作を担当している";
	mes "イングリッドと言います。";
	mes "よろしくお願いします。";
	next;
	if(Class != 28) {
		mes "[イングリッド]";
		mes "でも、残念ながら";
		mes "私が制作する武器は";
		mes "ガンスリンガー専用です。";
		next;
		mes "[イングリッド]";
		mes "お客様は";
		mes "ガンスリンガーギルドに";
		mes "加入していないので、";
		mes "私の武器を使うことはできません。";
		mes "申し訳ありません。";
		close;
	}
	mes "[イングリッド]";
	mes "まだ仕事を始めたばかりですが、";
	mes "一生懸命がんばります！";
	next;
	mes "[イングリッド]";
	mes "ああ……どきどきします。";
	mes "私はお客様のために";
	mes "インフェルノという武器を";
	mes "作っています。";
	next;
	mes "[イングリッド]";
	mes "インフェルノは";
	mes "ガンスリンガーギルドの研究所で";
	mes "開発された武器の中でも、";
	mes "最高の一品です。";
	next;
	mes "[イングリッド]";
	mes "高い破壊力を備えながら、";
	mes "人体工学的設計を考え、";
	mes "手に持った時の安定性。";
	mes "発砲の反動を最小限にした";
	mes "素晴らしい武器です！";
	mes "ふふふふふふ……";
	next;
	mes "[" +strcharinfo(0)+ "]";
	mes "……詳しいんですね。";
	next;
	mes "[イングリッド]";
	mes "当然です！";
	mes "その武器は私が設計";
	mes "したんですから。";
	next;
	mes "[" +strcharinfo(0)+ "]";
	mes "……なるほど……";
	next;
	mes "[イングリッド]";
	mes "……あー、んー";
	mes "ついつい自慢話に";
	mes "なってしまいましたね。";
	mes "セルレナ博士に比べると";
	mes "私なんて、まだまだ足元にも";
	mes "及ばないのに……";
	next;
	mes "[イングリッド]";
	mes "実は、私もガンスリンガー";
	mes "になりたかったんですが、";
	mes "身も心もついていけなくて、";
	mes "諦めちゃったんです。";
	next;
	mes "[イングリッド]";
	mes "でも、試験の間、";
	mes "セルレナ博士がずっと";
	mes "私の事を気にしてくれて、";
	mes "今のこの仕事ができるように";
	mes "色々と手伝ってくれたんです。";
	next;
	mes "[イングリッド]";
	mes "ガンスリンガーになれなかったのは";
	mes "ちょっと残念だけど、";
	mes "私と一緒に試験を受けた";
	mes "私のお兄ちゃんは";
	mes "ガンスリンガーになれました。";
	next;
	mes "[イングリッド]";
	mes "お兄ちゃんだけでも";
	mes "自分の夢を掴む事ができて、";
	mes "本当に嬉しいです。";
	next;
	mes "[イングリッド]";
	mes "アハハ。";
	mes "ごめんなさい。";
	mes "なんだか一人で勝手に";
	mes "しゃべってますね。";
	next;
	mes "[" +strcharinfo(0)+ "]";
	mes "いいんですよ。";
	mes "私が見る限り、イングリッドさんは";
	mes "ガンスリンガーになれなかった今でも";
	mes "充分、輝いているみたいです。";
	next;
	mes "[イングリッド]";
	mes "フフ、ありがとうございます。";
	mes "本当に、今の仕事は楽しいです。";
	mes "……さて、話を戻しましょうか。";
	next;
	mes "[イングリッド]";
	mes "インフェルノの製作には";
	mes "古い鉄板100個、錆びたネジ50個、";
	mes "オリデオコン10個、";
	mes "燃えている心臓100個と";
	mes "手数料20万Zenyが必要です。";
	mes "よろしいですか？";
	next;
	if(select("少し考えてみます","お願いします")==1) {
		mes "[イングリッド]";
		mes "わかりました。";
		mes "では、ゆっくりと";
		mes "考えてみてください。";
		close;
	}
	mes "[イングリッド]";
	mes "わかりました。";
	mes "では、材料が集まったら";
	mes "また来てください。";
	close;
}
